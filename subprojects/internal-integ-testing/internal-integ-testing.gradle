/*
 * Copyright 2011 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
dependencies {
    compile libraries.groovy
    compile project(":internalTesting")
    compile project(':cli')
    compile project(':launcher')
    compile project(':native')
    compile libraries.jetty
    compile module("org.littleshoot:littleproxy:1.1.0-beta1") {
        dependency libraries.slf4j_api
        dependency "io.netty:netty-all:4.0.23.Final"
        dependency "org.apache.commons:commons-lang3:3.4"
    }
    compile libraries.gcs
    compile libraries.ivy
    compile libraries.sshd
    compile libraries.gson
    compile libraries.joda
    compile libraries.jsch
    compile libraries.jcifs
    compile libraries.jansi
    compile libraries.commons_collections

    implementation project(':dependencyManagement')

    runtime project(':logging')
}

useTestFixtures(sourceSet: 'main')

task prepareVersionsInfo(type: PrepareVersionsInfo) {
    destFile = new File(generatedResourcesDir, "all-released-versions.properties")
    versions = releasedVersions.allVersions
    mostRecent = releasedVersions.mostRecentRelease
    mostRecentSnapshot = releasedVersions.mostRecentSnapshot
}

sourceSets.main.output.dir generatedResourcesDir, builtBy: prepareVersionsInfo

@CacheableTask
class PrepareVersionsInfo extends DefaultTask {
    @OutputFile File destFile
    @Input String mostRecent
    @Input List<String> versions
    @Input String mostRecentSnapshot

    @TaskAction void prepareVersions() {
        def properties = new Properties()
        properties.mostRecent = mostRecent
        properties.mostRecentSnapshot = mostRecentSnapshot
        properties.versions = versions.join(' ')

        org.gradle.build.ReproduciblePropertiesWriter.store(properties, destFile)
    }
}

apply from: "$rootDir/gradle/ideaTestSourcesWorkaround.gradle"
