/*
 * Copyright 2011 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
dependencies {
    compile libraries.groovy.coordinates
    compile project(":internalTesting")
    compile project(':cli')
    compile project(':launcher')
    compile project(':native')
    compile testLibraries.jetty
    compile module("org.littleshoot:littleproxy:1.1.0-beta1") {
        dependency libraries.slf4j_api.coordinates
        dependency "io.netty:netty-all:4.0.23.Final"
        dependency "org.apache.commons:commons-lang3:3.4"
    }
    compile libraries.gcs.coordinates, libraries.commons_httpclient.coordinates, libraries.joda.coordinates
    compile libraries.jackson_core.coordinates, libraries.jackson_annotations.coordinates, libraries.jackson_databind.coordinates
    compile libraries.ivy.coordinates
    compile testLibraries.sshd
    compile libraries.gson.coordinates
    compile libraries.joda.coordinates
    compile libraries.jsch.coordinates
    compile libraries.jcifs.coordinates
    compile libraries.jansi.coordinates
    compile libraries.commons_collections.coordinates
    compile 'org.apache.mina:mina-core'

    implementation project(':dependencyManagement')

    runtime project(':logging')
}

testFixtures {
    from(':core', 'main')
}

def generatedResourcesDir = gradlebuildJava.generatedResourcesDir

task prepareVersionsInfo(type: PrepareVersionsInfo) {
    destFile = new File(generatedResourcesDir, "all-released-versions.properties")
    versions = Versioning_extensionsKt.getReleasedVersions(project).allVersions
    mostRecent = Versioning_extensionsKt.getReleasedVersions(project).mostRecentRelease
    mostRecentSnapshot = Versioning_extensionsKt.getReleasedVersions(project).mostRecentSnapshot
}

sourceSets.main.output.dir generatedResourcesDir, builtBy: prepareVersionsInfo

@CacheableTask
class PrepareVersionsInfo extends DefaultTask {
    @OutputFile File destFile
    @Input String mostRecent
    @Input List<String> versions
    @Input String mostRecentSnapshot

    @TaskAction void prepareVersions() {
        def properties = new Properties()
        properties.mostRecent = mostRecent
        properties.mostRecentSnapshot = mostRecentSnapshot
        properties.versions = versions.join(' ')

        org.gradle.build.ReproduciblePropertiesWriter.store(properties, destFile)
    }
}

ideConfiguration {
    makeAllSourceDirsTestSourceDirsToWorkaroundIssuesWithIDEA13()
}
