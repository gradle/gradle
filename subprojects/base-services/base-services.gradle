/*
 * A set of generic services and utilities.
 *
 * Should have a very small set of dependencies, and should be appropriate to embed in an external
 * application (eg as part of the tooling API).
 */
plugins {
    id 'java-library'
    id 'classycle'
}

sourceCompatibility = javaVersion.java9Compatible ? 1.6 : 1.5

dependencies {
    api libraries.guava
    api libraries.jsr305
    api libraries.fastutil

    implementation libraries.slf4j_api
    implementation libraries.commons_lang
    implementation libraries.commons_io
    implementation libraries.jcip

    jmh libraries.bouncycastle_provider
}

jmh {
    include = ["HashingAlgorithmsBenchmark"]
}

task buildReceiptResource(type: Copy) {
    from {
        tasks.getByPath(':createBuildReceipt').outputs.files
    }
    destinationDir = file("${generatedTestResourcesDir}/${buildReceiptPackage}")
}
sourceSets.main.output.dir generatedTestResourcesDir, builtBy: buildReceiptResource

useTestFixtures()
