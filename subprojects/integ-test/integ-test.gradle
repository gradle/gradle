import org.gradle.os.OperatingSystem

dependencies {
    groovy libraries.groovy

    integTestCompile project(':toolingApi')
    integTestCompile project(':launcher')
    integTestCompile project(':coreImpl')
    integTestCompile libraries.ant
    integTestCompile libraries.xmlunit

    integTestRuntime rootProject.configurations.testRuntime.allDependencies
}

integTestTasks.all {
    dependsOn ':publishLocalArchives', ':binZip', ':allZip', ':srcZip', ':docs:userguideDocbook'

    jvmArgs '-Xmx512m', '-XX:MaxPermSize=256m'

    doFirst {
        systemProperties['integTest.userGuideInfoDir'] = project(':docs').docbookSrc
        systemProperties['integTest.userGuideOutputDir'] = new File(project(':docs').samplesSrcDir, "userguideOutput").absolutePath
        systemProperties['integTest.distsDir'] = rootProject.distsDir.absolutePath
        systemProperties['integTest.libsRepo'] = rootProject.file('build/repo')
        forkEvery = 15

        if (isDevBuild()) {
            exclude 'org/gradle/integtests/DistributionIntegrationTest.*'
            exclude 'org/gradle/integtests/tooling/**/*Suite.*'
        }
    }
}

daemonIntegTest {
    exclude "**/org/gradle/integtests/tooling/**", //tooling integ tests use daemon anyway, don't rerun
            "**/CrossVersionCompatibilityIntegrationTest.class", //ignored just in case to avoid old daemon implementation
            "**/DistributionIntegrationTest.class" //fragile - heavily depends on external repos and does not contribute too much to the daemon suite anyway
}

integTest {
    if (OperatingSystem.current().isWindows()) {
        //Temporary hack to investigate windows build issues
        exclude "**/org/gradle/integtests/tooling/**"
    }
}
