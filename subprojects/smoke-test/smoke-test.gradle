import org.gradle.testing.SmokeTest

useTestFixtures()

sourceSets {
    smokeTest {
        compileClasspath += main.output + test.output
        runtimeClasspath += compileClasspath
    }
}

configurations {
    smokeTestCompile.extendsFrom testCompile
    smokeTestRuntime.extendsFrom testRuntime
}

dependencies {
    smokeTestCompile project(':testKit')
    smokeTestCompile libraries.spock
}

processSmokeTestResources {
    from ("$rootDir/gradle") {
        into "org/gradle/smoketests/cache-init-scripts"
        include "overlappingTaskOutputsStatsInit.gradle"
        include "taskCacheDetailedStatsInit.gradle"
    }
}

tasks.create('smokeTest', SmokeTest) { SmokeTest task ->
    group = "Verification"
    description = "Runs Smoke tests"
    testClassesDir = sourceSets.smokeTest.output.classesDir
    classpath = sourceSets.smokeTest.runtimeClasspath
}

plugins.withType(org.gradle.plugins.ide.idea.IdeaPlugin) { // lazy as plugin not applied yet
    idea {
        module {
            sourceSets.smokeTest.allSource.srcDirs.each {
                testSourceDirs += it
            }
            scopes.TEST.plus.add(configurations.smokeTestCompile)
            scopes.TEST.plus.add(configurations.smokeTestRuntime)
        }
    }
}

plugins.withType(org.gradle.plugins.ide.eclipse.EclipsePlugin) { // lazy as plugin not applied yet
    eclipse {
        classpath {
            plusConfigurations.add(configurations.smokeTestCompile)
            plusConfigurations.add(configurations.smokeTestRuntime)
        }
    }
}
