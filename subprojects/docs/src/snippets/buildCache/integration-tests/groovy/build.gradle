plugins {
    id('java-library')
}

sourceSets {
    integTest
}

tasks.register("integTest", Test) {
    classpath = sourceSets.integTest.runtimeClasspath
    testClassesDirs = sourceSets.integTest.output.classesDirs
}

// tag::integTest[]
tasks.named('integTest') {
    inputs.property("operatingSystem") {
        System.getProperty("os.name")
    }
}
// end::integTest[]

// tag::distributionPathInput[]
// Don't do this! Breaks relocatability!
tasks.named('integTest') {
    systemProperty "distribution.location", layout.buildDirectory.dir('dist').get().asFile.absolutePath
}
// end::distributionPathInput[]

// tag::distributionDirInput[]
class DistributionLocationProvider implements CommandLineArgumentProvider { // <1>
    @InputDirectory
    @PathSensitive(PathSensitivity.RELATIVE)                                // <2>
    File distribution

    @Override
    Iterable<String> asArguments() {
        ["-Ddistribution.location=${distribution.absolutePath}".toString()] // <3>
    }
}

tasks.named('integTest') {
    jvmArgumentProviders.add(
        new DistributionLocationProvider(distribution: layout.buildDirectory.dir('dist').get().asFile) // <4>
    )
}
// end::distributionDirInput[]

// tag::ignoreSystemProperties[]
class CiEnvironmentProvider implements CommandLineArgumentProvider {
    @Internal                                                        // <1>
    String agentNumber = System.getenv().get("AGENT_NUMBER") ?: "1"

    @Override
    Iterable<String> asArguments() {
        ["-DagentNumber=${agentNumber}".toString()]                  // <2>
    }
}

tasks.named('integTest') {
    jvmArgumentProviders.add(
        new CiEnvironmentProvider()                                  // <3>
    )
}
// end::ignoreSystemProperties[]

// tag::environment[]
tasks.named('integTest') {
    inputs.property("langEnvironment") {
        System.getenv("LANG")
    }
}
// end::environment[]
