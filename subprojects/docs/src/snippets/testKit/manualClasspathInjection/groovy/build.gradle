plugins {
    id 'groovy'
}

dependencies {
    implementation localGroovy()
    implementation gradleApi()
    testImplementation gradleTestKit()
    testImplementation('org.spockframework:spock-core:2.0-M4-groovy-3.0') {
        exclude module: 'groovy-all'
    }
    testImplementation('org.junit.jupiter:junit-jupiter-api')
}

repositories {
    mavenCentral()
}

// tag::test-logic-classpath[]
// Write the plugin's classpath to a file to share with the tests
task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files(sourceSets.main.runtimeClasspath)
        .withPropertyName("runtimeClasspath")
        .withNormalizer(ClasspathNormalizer)
    outputs.dir(outputDir)
        .withPropertyName("outputDir")

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

// Add the classpath file to the test runtime classpath
dependencies {
    testRuntimeOnly files(createClasspathManifest)
}
// end::test-logic-classpath[]
