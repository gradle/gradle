plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group = 'gradle'

// tag::conditional-signing[]
version = '1.0-SNAPSHOT'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

// end::conditional-signing[]
publishing {
    publications {
        main(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = "localRepo"
            url = uri(layout.buildDirectory.dir("repo"))
        }
    }
}

// tag::conditional-signing[]
signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("publish") }
    sign publishing.publications.main
}
// end::conditional-signing[]

// Alternative to signing.required
// tag::only-if[]
tasks.withType(Sign) {
    onlyIf("isReleaseVersion is set") { isReleaseVersion }
}
// end::only-if[]
