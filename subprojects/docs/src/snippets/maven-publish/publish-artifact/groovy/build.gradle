plugins {
    id 'base'
    id 'maven-publish'
}

group = 'org.gradle.sample'
version = '1.0'

// tag::custom-artifact[]
configurations {
    conf
}
def rpmFile = layout.buildDirectory.file('rpms/my-package.rpm')
def rpmArtifact = artifacts.add('conf', rpmFile.get().asFile) {
    type 'rpm'
    builtBy 'rpm'
}
// end::custom-artifact[]

tasks.register('rpm') {
    outputs.file rpmFile
    doLast {
        // produce real RPM here
        rpmFile.get().asFile << 'file contents'
    }
}

// tag::custom-artifact-publication[]
publishing {
    publications {
        maven(MavenPublication) {
            artifact rpmArtifact
        }
    }
// end::custom-artifact-publication[]
    repositories {
        // change URLs to point to your repo, e.g. http://my.org/repo
        maven {
            url = layout.buildDirectory.dir('repo')
        }
    }
// tag::custom-artifact-publication[]
}
// end::custom-artifact-publication[]
