apply plugin: 'java'

if (hasProperty('showOutput')) {

// START SNIPPET access-source-set
// Various ways to access the main source set
println sourceSets.main.output.classesDir
println sourceSets['main'].output.classesDir
sourceSets {
    println main.output.classesDir
}
sourceSets {
    main {
        println output.classesDir
    }
}

// Iterate over the source sets
sourceSets.all {
    println name
}
// END SNIPPET access-source-set

}

// START SNIPPET define-source-set
sourceSets {
    intTest
}
// END SNIPPET define-source-set

repositories {
    mavenCentral()
}

// START SNIPPET source-set-dependencies
sourceSets {
    intTest
}

dependencies {
    intTestCompile 'junit:junit:4.12'
    intTestRuntime 'org.ow2.asm:asm-all:4.0'
}
// END SNIPPET source-set-dependencies

// START SNIPPET jar
task intTestJar(type: Jar) {
    from sourceSets.intTest.output
}
// END SNIPPET jar

// START SNIPPET javadoc
task intTestJavadoc(type: Javadoc) {
    source sourceSets.intTest.allJava
}
// END SNIPPET javadoc

// START SNIPPET test
task intTest(type: Test) {
    testClassesDir = sourceSets.intTest.output.classesDir
    classpath = sourceSets.intTest.runtimeClasspath
}
// END SNIPPET test
