subprojects {
    version = '1.0.2'
    group = 'org.gradle.sample'

    repositories {
        jcenter()
    }

    plugins.withType(JavaPlugin).configureEach {
        java {
            modularity.inferModulePath = true
        }

        sourceSets {
            integrationTest
        }

        configurations {
            integrationTestImplementation.extendsFrom implementation
            integrationTestRuntimeOnly.extendsFrom runtimeOnly
        }

        dependencies {
            integrationTestImplementation project(path)
            integrationTestImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.1'
            integrationTestRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
        }

        def integrationTestJarTask = tasks.register(sourceSets.integrationTest.jarTaskName, Jar) {
            archiveClassifier = 'integration-tests'
            from sourceSets.integrationTest.output
        }
        def integrationTestTask = tasks.register('integrationTest', Test) {
            description = 'Runs integration tests.'
            group = 'verification'

            testClassesDirs = sourceSets.integrationTest.output.classesDirs
            // Make sure we run the 'Jar' containing the tests (and not just the 'classes' folder) so that test resources are also part of the test module
            classpath = configurations[sourceSets.integrationTest.runtimeClasspathConfigurationName] + files(integrationTestJarTask)
            shouldRunAfter('test')
        }

        tasks.named('check') { dependsOn(integrationTestTask) }
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
    }
}
