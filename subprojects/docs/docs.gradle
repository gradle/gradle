
/*
 * Copyright 2010 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
import org.gradle.docs.samples.internal.tasks.InstallSample
import org.gradle.gradlebuild.test.integrationtests.GradleDistribution

plugins {
    id 'gradlebuild.internal.java'
    // TODO: Apply asciidoctor in documentation plugin instead.
    id 'org.asciidoctor.convert'
    id 'gradlebuild.documentation'
    id 'gradlebuild.unittest-and-compile'
    id 'org.gradle.samples' version "0.16.8"
}

repositories { handler ->
    // TODO migrate this build script to Kotlin DSL to replace this is a simple googleApisJs()
    Plugins_extensionsKt.googleApisJs(handler)
}

configurations {
    gradleFullDocsElements {
        attributes {
            attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage, Usage.JAVA_RUNTIME))
            attribute(Category.CATEGORY_ATTRIBUTE, objects.named(Category, Category.DOCUMENTATION))
            attribute(DocsType.DOCS_TYPE_ATTRIBUTE, objects.named(DocsType, "gradle-documentation"))
        }
        visible = false
        canBeResolved = false
        canBeConsumed = true
    }
    docsTestRuntimeClasspath.extendsFrom(integTestDistributionRuntimeOnly)
}

dependencies {
    // generate Javadoc for the full Gradle distribution
    runtimeOnly project(":distributionsFull")

    asciidoctor "org.gradle:docs-asciidoctor-extensions:0.4.0"
    // TODO: Why isn't this using the output instead?
    asciidoctor project.files("src/main/resources")

    userGuideTask 'xalan:xalan:2.7.1'
    userGuideTask 'xerces:xercesImpl:2.11.0'
    userGuideTask 'xhtmlrenderer:xhtmlrenderer:R8rc1'
    userGuideTask 'itext:itext:2.0.8'
    userGuideTask 'xslthl:xslthl:2.0.1'

    userGuideStyleSheets 'docbook:docbook-xsl:1.75.2@zip'

    jquery "jquery:jquery.min:3.4.1@js"
    jquery "com.drewwilson.code:jquery.tipTip:1.3:minified@js"

    testImplementation project(":baseServices")
    testImplementation project(":core")
    testImplementation testLibraries.jsoup
    testImplementation "org.gebish:geb-spock:2.2"
    testImplementation 'org.seleniumhq.selenium:selenium-htmlunit-driver:2.42.2'
    testImplementation libraries.commons_httpclient.coordinates
    testImplementation "org.apache.httpcomponents:httpmime:${libraries.commons_httpclient.version}"

    docsTestImplementation project(":internalIntegTesting")
    docsTestImplementation project(":testKit")
    docsTestImplementation "${libraries.junit5_vintage.coordinates}:${libraries.junit5_vintage.version}"
    docsTestImplementation "${libraries.junit.coordinates}:${libraries.junit.version}"

    integTestDistributionRuntimeOnly project(":distributionsFull")
}

asciidoctorj {
    version = '1.5.8.1'
    noDefaultRepositories = true
}

gradleDocumentation {
    javadocs {
        javaApi = project.uri("https://docs.oracle.com/javase/8/docs/api")
        groovyApi = project.uri("https://docs.groovy-lang.org/docs/groovy-${groovyVersion}/html/gapi")
    }
}

tasks.named("stageDocs") {
    // Add samples to generated documentation
    from(samples.distribution.renderedDocumentation) {
        into("samples")
    }
}

samples {
    templates {
        javaAndroidApplication
        springBootWebApplication
        gradlePluginInJava
        buildSrcPluginJavaModuleTransform

        javaApplication
        javaListLibrary
        javaUtilitiesLibrary
        javaApplicationAsSubproject {
            sourceDirectory = javaApplication.sourceDirectory
            target = "application"
        }
        javaListLibraryAsSubproject {
            sourceDirectory = javaListLibrary.sourceDirectory
            target = "list"
        }
        javaUtilitiesLibraryAsSubproject {
            sourceDirectory = javaUtilitiesLibrary.sourceDirectory
            target = "utilities"
        }

        groovySpockTestForListLibrary {
            target = "list"
        }
        groovySpockTestForApplication {
            target = "application"
        }
        javaJunit5TestForApplication {
            target = "application"
        }
        javaJunit5TestForListLibrary {
            target = "list"
        }
        javaJunit5IntegrationTestForApplication {
            target = "application"
        }
        javaJunit5IntegrationTestForUtilitiesLibrary {
            target = "utilities"
        }

        javaModuleInfoForListLibrary {
            target = "list"
        }
        javaModuleInfoForUtilitiesLibrary {
            target = "utilities"
        }
        javaModuleInfoForApplication {
            target = "application"
        }
        javaJunit5ModuleInfoForUtilitiesLibrary {
            target = "utilities"
        }
        javaJunit5ModuleInfoForApplication {
            target = "application"
        }

        groovyApplication
        groovyListLibrary
        groovyUtilitiesLibrary
        groovyApplicationAsSubproject {
            sourceDirectory = groovyApplication.sourceDirectory
            target = "application"
        }
        groovyListLibraryAsSubproject {
            sourceDirectory = groovyListLibrary.sourceDirectory
            target = "list"
        }
        groovyUtilitiesLibraryAsSubproject {
            sourceDirectory = groovyUtilitiesLibrary.sourceDirectory
            target = "utilities"
        }

        kotlinApplication
        kotlinListLibrary
        kotlinUtilitiesLibrary

        projectInfoPlugin

        precompiledScriptPluginGroovy {
            target = "plugin"
        }
        precompiledScriptPluginGroovyInBuildSrc {
            sourceDirectory = precompiledScriptPluginGroovy.sourceDirectory
            target = "buildSrc"
        }

        precompiledScriptPluginKotlin {
            target = "plugin"
        }
        precompiledScriptPluginKotlinInBuildSrc {
            sourceDirectory = precompiledScriptPluginKotlin.sourceDirectory
            target = "buildSrc"
        }

        precompiledScriptPluginUtils {
            target = "plugin"
        }
        precompiledScriptPluginUtilsInBuildSrc {
            sourceDirectory = precompiledScriptPluginUtils.sourceDirectory
            target = "buildSrc"
        }
    }

    // TODO: Do this lazily so we don't need to walk the filesystem during configuration
    // iterate through each snippets and record their names and locations
    FileFilter directoriesOnly = { it.directory }
    def topLevelDirs = file('src/snippets').listFiles(directoriesOnly)
    def snippetDirs = topLevelDirs*.listFiles(directoriesOnly).flatten()

    snippetDirs.each { File snippetDir ->
        String snippetName = snippetDir.name
        String categoryName = snippetDir.parentFile.name
        def id = org.gradle.docs.internal.StringUtils.toLowerCamelCase("snippet-" + categoryName + "-" + snippetName)
        publishedSamples.create(id) {
            description = "Snippet from $snippetDir"
            category = "Other"
            readmeFile = file("src/snippets/default-readme.adoc")
            sampleDirectory = snippetDir
            promoted = false
        }
    }

    publishedSamples {
        androidApplication {
            description = "Build a simple Android application."
            category = "Android"
            common {
                from(templates.javaAndroidApplication)
            }
        }
        springBootWebApplication {
            description = "Build a simple Spring Boot application."
            category = "Spring"
            common {
                from(templates.springBootWebApplication)
            }
        }

        jvmMultiProjectWithAdditionalTestTypes {
            sampleDirectory = samplesRoot.dir("java/jvm-multi-project-with-additional-test-types")
            displayName = "Using additional test types"
            description = "Add an additional test type (e.g. integration tests) to a project."
            category = "Java"

            common {
                from(templates.javaApplicationAsSubproject)
                from(templates.javaJunit5TestForApplication)
                from(templates.javaJunit5IntegrationTestForApplication)

                from(templates.javaListLibraryAsSubproject)
                from(templates.javaJunit5TestForListLibrary)
                from(templates.javaJunit5IntegrationTestForUtilitiesLibrary)

                from(templates.javaUtilitiesLibraryAsSubproject)
            }
        }
        javaApplication {
            sampleDirectory = samplesRoot.dir("java/application")
            description = "Build a Java application."
            category = "Java"
            common {
                from(templates.javaApplication)
                from(templates.javaListLibrary)
                from(templates.javaUtilitiesLibrary)
            }
        }
        javaLibrary {
            sampleDirectory = samplesRoot.dir("java/library")
            description = "Build a Java library."
            category = "Java"
            common {
                from(templates.javaListLibrary)
                from(templates.javaUtilitiesLibrary)
            }
        }
        javaMultiProjectWithSpockTests {
            sampleDirectory = samplesRoot.dir("java/multi-project-with-spock-tests")
            displayName = "Testing with Spock"
            description = "Test a Java application and library with link:https://github.com/spockframework/spock[Spock]."
            category = "Java"
            common {
                from(templates.javaApplicationAsSubproject)
                from(templates.javaListLibraryAsSubproject)
                from(templates.javaUtilitiesLibraryAsSubproject)
                from(templates.groovySpockTestForApplication)
                from(templates.groovySpockTestForListLibrary)
            }
        }
        javaMultiProjectWithJUnit5Tests {
            sampleDirectory = samplesRoot.dir("java/multi-project-with-junit-5-tests")
            displayName = "Testing with JUnit5"
            description = "Test a Java application and library with link:https://junit.org/junit5[JUnit5]."
            category = "Java"
            common {
                from(templates.javaApplicationAsSubproject)
                from(templates.javaListLibraryAsSubproject)
                from(templates.javaUtilitiesLibraryAsSubproject)
                from(templates.javaJunit5TestForApplication)
                from(templates.javaJunit5TestForListLibrary)
            }
        }
        javaModulesMultiProject {
            sampleDirectory = samplesRoot.dir("java/modules-multi-project")
            displayName = "Java Modules"
            description = "Build Java Modules and a modular Java application."
            category = "Java Modules"
            common {
                from(templates.javaApplicationAsSubproject)
                from(templates.javaListLibraryAsSubproject)
                from(templates.javaUtilitiesLibraryAsSubproject)
                from(templates.javaModuleInfoForListLibrary)
                from(templates.javaModuleInfoForUtilitiesLibrary)
                from(templates.javaModuleInfoForApplication)
                from(templates.javaJunit5TestForApplication)
                from(templates.javaJunit5TestForListLibrary)
            }
        }
        javaModulesMultiProjectWithIntegrationTests {
            sampleDirectory = samplesRoot.dir("java/modules-multi-project-with-integration-tests")
            displayName = "Java Modules with Blackbox Tests"
            description = "Build Java Modules with blackbox integration tests."
            category = "Java Modules"
            common {
                from(templates.javaApplicationAsSubproject)
                from(templates.javaListLibraryAsSubproject)
                from(templates.javaUtilitiesLibraryAsSubproject)
                from(templates.javaModuleInfoForListLibrary)
                from(templates.javaModuleInfoForUtilitiesLibrary)
                from(templates.javaModuleInfoForApplication)
                from(templates.javaJunit5TestForApplication)
                from(templates.javaJunit5TestForListLibrary)

                from(templates.javaJunit5IntegrationTestForApplication)
                from(templates.javaJunit5ModuleInfoForApplication)
                from(templates.javaJunit5IntegrationTestForUtilitiesLibrary)
                from(templates.javaJunit5ModuleInfoForUtilitiesLibrary)
            }
        }
        javaModulesWithTransform {
            sampleDirectory = samplesRoot.dir("java/modules-with-transform")
            displayName = "Java Modules with Legacy Libraries"
            description = "Build a modular Java application that integrates legacy libraries."
            category = "Java Modules"
            common {
                from(templates.buildSrcPluginJavaModuleTransform)
            }
        }
        jvmMultiProjectWithCodeCoverage {
            sampleDirectory = samplesRoot.dir("java/jvm-multi-project-with-code-coverage")
            displayName = "Reporting code coverage with JaCoCo"
            description = "Report code coverage on a multi-module project using link:https://www.jacoco.org/jacoco/[JaCoCo]."
            category = "Java"
            common {
                from(templates.javaApplicationAsSubproject)
                from(templates.javaListLibraryAsSubproject)
                from(templates.javaUtilitiesLibraryAsSubproject)
                from(templates.javaJunit5TestForApplication)
                from(templates.javaJunit5TestForListLibrary)
            }
        }
        javaLibraryPublishing {
            sampleDirectory = samplesRoot.dir("java/library-publishing")
            description = "Publish a Java library to a binary repository."
            category = "Java"
            common {
                from(templates.javaListLibrary)
                from(templates.javaUtilitiesLibrary)
            }
        }

        kotlinApplication {
            sampleDirectory = samplesRoot.dir("kotlin/application")
            description = "Build a Kotlin application."
            category = "Kotlin"
            common {
                from(templates.kotlinApplication)
                from(templates.kotlinListLibrary)
                from(templates.kotlinUtilitiesLibrary)
            }
        }
        kotlinLibrary {
            sampleDirectory = samplesRoot.dir("kotlin/library")
            description = "Build a Kotlin library."
            category = "Kotlin"
            common {
                from(templates.kotlinListLibrary)
                from(templates.kotlinUtilitiesLibrary)
            }
        }

        groovyApplication {
            sampleDirectory = samplesRoot.dir("groovy/application")
            description = "Build a Groovy application."
            category = "Groovy"
            common {
                from(templates.groovyApplication)
                from(templates.groovyListLibrary)
                from(templates.groovyUtilitiesLibrary)
            }
        }
        groovyLibrary {
            sampleDirectory = samplesRoot.dir("groovy/library")
            description = "Build a Groovy library."
            category = "Groovy"
            common {
                from(templates.groovyListLibrary)
                from(templates.groovyUtilitiesLibrary)
            }
        }
        groovyMultiProjectWithSpockTests {
            sampleDirectory = samplesRoot.dir("groovy/multi-project-with-spock-tests")
            displayName = "Testing with Spock"
            description = "Test a Groovy application and library with link:https://github.com/spockframework/spock[Spock]."
            category = "Groovy"
            common {
                from(templates.groovyApplicationAsSubproject)
                from(templates.groovyListLibraryAsSubproject)
                from(templates.groovyUtilitiesLibraryAsSubproject)
                from(templates.groovySpockTestForApplication)
                from(templates.groovySpockTestForListLibrary)
            }
        }
        groovyLibraryPublishing {
            sampleDirectory = samplesRoot.dir("groovy/library-publishing")
            description = "Publish a Groovy library to a binary repository."
            category = "Groovy"
            common {
                from(templates.groovyListLibrary)
                from(templates.groovyUtilitiesLibrary)
            }
        }

        gradlePlugin {
            sampleDirectory = samplesRoot.dir("build-organization/gradle-plugin")
            description = "Organize your build logic into a Gradle plugin written in Java."
            category = "Build organization"
            common {
                from(templates.gradlePluginInJava)
            }
        }

        precompiledScriptPlugin {
            sampleDirectory = samplesRoot.dir("build-organization/precompiled-script-plugin")
            description = "Use the Groovy or Kotlin DSL in a Gradle plugin."
            category = "Build organization"
            common {
                from(templates.precompiledScriptPluginUtils)
            }
            groovy {
                from(templates.precompiledScriptPluginGroovy)
            }
            kotlin {
                from(templates.precompiledScriptPluginKotlin)
            }
        }

        precompiledScriptPluginInBuildSrc {
            sampleDirectory = samplesRoot.dir("build-organization/multi-project-with-precompiled-script-plugins")
            displayName = "Precompiled Script Plugin in buildSrc"
            description = "Use the Groovy or Kotlin DSL in a Gradle plugin in buildSrc."
            category = "Build organization"
            common {
                from(templates.precompiledScriptPluginUtilsInBuildSrc)
            }
            groovy {
                from(templates.precompiledScriptPluginGroovyInBuildSrc)
            }
            kotlin {
                from(templates.precompiledScriptPluginKotlinInBuildSrc)
            }
        }

        jvmMultiProjectBuild {
            sampleDirectory = samplesRoot.dir("build-organization/jvm-multi-project-build")
            displayName = "Multi-project build (JVM)"
            description = "Use dependencies in a multi-project build."
            category = "Build organization"
            common {
                from(templates.javaApplicationAsSubproject)
                from(templates.javaListLibraryAsSubproject)
                from(templates.javaUtilitiesLibraryAsSubproject)
            }
        }

        taskWithArguments {
            sampleDirectory = samplesRoot.dir("build-organization/task-with-arguments")
            displayName = "Task with command-line arguments"
            description = "Pass arguments to a custom task."
            category = "Build organization"
        }

        publishingCredentials {
            sampleDirectory = samplesRoot.dir("credentials-handling/publishing-credentials")
            description = "Publish to a password protected repository"
            category = "Using Credentials"
            common {
                from(templates.javaListLibrary)
                from(templates.javaUtilitiesLibrary)
            }
        }

        credentialsForExternalToolViaStdin {
            sampleDirectory = samplesRoot.dir("credentials-handling/pass-credentials-to-external-tool-via-stdin")
            displayName = "Supply credentials to external tool"
            description = "Pass credentials to an external tool via stdin using Gradle properties."
            category = "Using Credentials"
        }

        compositeBuildsBasics {
            sampleDirectory = samplesRoot.dir("build-organization/composite-builds/basic")
            description = "Defining and using a composite build"
            category = "Build organization"
        }

        compositeBuildsDeclaredSubstitutions {
            sampleDirectory = samplesRoot.dir("build-organization/composite-builds/declared-substitution")
            description = "Applying and testing changes in downstream dependencies without publishing."
            category = "Build organization"
        }

        compositeBuildsHierarchicalMultiRepo {
            sampleDirectory = samplesRoot.dir("build-organization/composite-builds/hierarchical-multirepo")
            description = "Defining and using a composite build to combine multiple independent builds."
            category = "Build organization"
        }

        compositeBuildsPluginDevelopment {
            sampleDirectory = samplesRoot.dir("build-organization/composite-builds/plugin-dev")
            description = "Developing a Gradle plugin in a build without publishing."
            category = "Build organization"
        }
    }
}

// Use the version of Gradle being built, not the version of Gradle used to build
tasks.named("generateWrapperForSamples") {
    gradleVersion = project.version
}

// TODO: The rich console to plain text is flaky
tasks.named("checkAsciidoctorSampleContents") {
    enabled = false
}

// exclude (unused and non-existing) wrapper of development Gradle version, as well as README, because the timestamp in the Gradle version break the cache
tasks.withType(InstallSample) {
    if (name.contains('ForTest')) {
        excludes.add("gradle/wrapper/**")
        excludes.add("README")
    }
}

// TODO add some kind of test precondition support in sample test conf
tasks.named("docsTest") { task ->
    // The org.gradle.samples plugin uses Exemplar to execute integration tests on the samples.
    // Exemplar doesn't know about that it's running in the context of the gradle/gradle build
    // so it uses the Gradle distribution from the running build. This is not correct, because
    // we want to verify that the samples work with the Gradle distribution being built.
    def installationEnvProvider = new GradleInstallationForTestEnvironmentProvider(project, task)
    installationEnvProvider.gradleHomeDir.from(configurations.integTestDistributionRuntimeClasspath)
    installationEnvProvider.samplesdir.set(project.layout.buildDirectory.dir("working/samples/testing"))
    jvmArgumentProviders.add(installationEnvProvider)

    // For unknown reason, this is set to 'sourceSet.getRuntimeClasspath()' in the 'org.gradle.samples' plugin
    testClassesDirs = sourceSets.docsTest.output.classesDirs
    // 'integTest.samplesdir' is set to an absolute path by the 'org.gradle.samples' plugin
    systemProperties.clear()

    // // TODO (donat) investigate ignored snippets
    filter {
        // Do not execute Java Module tests on JVM < 9
        if (!buildJvms.testJvm.get().javaVersion.java9Compatible) {
            excludeTestsMatching "org.gradle.docs.samples.ExemplarExternalSamplesFunctionalTest.java-modules-*.sample"
        }

        excludeTestsMatching 'org.gradle.docs.samples.ExemplarExternalSamplesFunctionalTest.android-application_*.sample' // has build deprecations
        excludeTestsMatching 'org.gradle.docs.samples.ExemplarExternalSamplesFunctionalTest.snippet-best-practices-logic-during-configuration-dont_*.sample' // negative sanity check fails (= build succeeds) for Kotlin variant
        excludeTestsMatching 'org.gradle.docs.samples.ExemplarExternalSamplesFunctionalTest.snippet-scala-cross-compilation_groovy_sanityCheck.sample' // There is no java executable in /Library/Java/JavaVirtualMachines/1.6.0.jdk/Contents/Home/bin. Expression: executable.exists()
        excludeTestsMatching 'org.gradle.docs.samples.ExemplarExternalSamplesFunctionalTest.snippet-composite-builds*.sample' // emits deprecation warning
        excludeTestsMatching 'org.gradle.docs.samples.ExemplarExternalSamplesFunctionalTest.snippet-groovy-cross-compilation_*.sample'  // compilation error
        excludeTestsMatching 'org.gradle.docs.samples.ExemplarExternalSamplesFunctionalTest.snippet-init-scripts-custom-logger_*.sample' // seems legit: Smart cast to 'Throwable' is impossible, because 'result.failure' is a property that has open or custom getter
        excludeTestsMatching 'org.gradle.docs.samples.ExemplarExternalSamplesFunctionalTest.snippet-kotlin-dsl-android-build_*.sample' // plugin [id: 'com.android.application', version: '3.2.0', apply: false] was not found: Gradle Central Plugin Repository
        excludeTestsMatching 'org.gradle.docs.samples.ExemplarExternalSamplesFunctionalTest.snippet-kotlin-dsl-android-single-build_*.sample' // seems legit: java.lang.NoClassDefFoundError: Could not initialize class org.jetbrains.kotlin.gradle.plugin.sources.DefaultKotlinSourceSetKt
        excludeTestsMatching 'org.gradle.docs.samples.ExemplarExternalSamplesFunctionalTest.snippet-kotlin-dsl-multi-project-build_*.sample'  // emits deprecation warning
        excludeTestsMatching 'org.gradle.docs.samples.ExemplarExternalSamplesFunctionalTest.snippet-multiproject-flat-with-no-default-master_groovy_sanityCheck.sample' // emits deprecation warning
        excludeTestsMatching 'org.gradle.docs.samples.ExemplarExternalSamplesFunctionalTest.snippet-multiproject-flat_*.sample' // emits deprecation warning
        excludeTestsMatching 'org.gradle.docs.samples.ExemplarExternalSamplesFunctionalTest.snippet-plugins-consuming_*.sample' // emits deprecation warning
        excludeTestsMatching 'org.gradle.docs.samples.ExemplarExternalSamplesFunctionalTest.snippet-plugins-custom-plugin_*.sample' // emits deprecation warning
        excludeTestsMatching 'org.gradle.docs.samples.ExemplarExternalSamplesFunctionalTest.snippet-plugins-multiproject_*.sample' // emits deprecation warning
        excludeTestsMatching 'org.gradle.docs.samples.ExemplarExternalSamplesFunctionalTest.snippet-plugins-plugin-versions_*.sample' // emits deprecation warning
        excludeTestsMatching 'org.gradle.docs.samples.ExemplarExternalSamplesFunctionalTest.snippet-plugins-resolution-rules_*.sample' // emits deprecation warning
        excludeTestsMatching 'org.gradle.docs.samples.ExemplarExternalSamplesFunctionalTest.snippet-tasks-custom-task-using-convention_*.sample' // seems legit: Could not set unknown property 'greeting' for root project 'custom-task-using-convention' of Project.
        excludeTestsMatching 'org.gradle.docs.samples.ExemplarExternalSamplesFunctionalTest.snippet-tooling-api-custom-model_*.sample' // emits deprecation warning
        excludeTestsMatching 'org.gradle.docs.samples.ExemplarExternalSamplesFunctionalTest.snippet-tutorial-archive-content_*.sample'  // seems like a legit failure
        excludeTestsMatching 'org.gradle.docs.samples.ExemplarExternalSamplesFunctionalTest.snippet-tutorial-select-project_*.sample' // emits deprecation warning
        excludeTestsMatching 'org.gradle.docs.samples.ExemplarExternalSamplesFunctionalTest.snippet-java-cross-compilation_*.sample' // fails to find javadoc
    }
}

// Publications for the docs subproject:

configurations {
    gradleFullDocsElements {
        // TODO: This breaks the provider
        outgoing.artifact(project.gradleDocumentation.getDocumentationRenderedRoot().get().asFile) {
            builtBy 'docs'
        }
    }
}

tasks.named('platformTest') {
    dependsOn(tasks.named('checkSamples'))
}

// TODO there is some duplication with DistributionTest.kt here - https://github.com/gradle/gradle-private/issues/3126
class GradleInstallationForTestEnvironmentProvider implements CommandLineArgumentProvider {
    private Project project
    private Test testTask

    GradleInstallationForTestEnvironmentProvider(Project project, Test testTask) {
        this.project = project
        this.testTask = testTask
    }

    @Internal
    final ConfigurableFileCollection gradleHomeDir = project.objects.fileCollection()

    @PathSensitive(PathSensitivity.RELATIVE)
    @InputDirectory
    final DirectoryProperty samplesdir = project.objects.directoryProperty()

    @Nested
    GradleDistribution gradleDistribution = new GradleDistribution(gradleHomeDir)

    @Override
    Iterable<String> asArguments() {
        def distributionName = testTask.classpath.filter { it.name.startsWith("gradle-runtime-api-info") }.singleFile.parentFile.parentFile.parentFile.name
        ["-DintegTest.gradleHomeDir=${gradleHomeDir.singleFile}",
         "-DintegTest.samplesdir=${samplesdir.get().asFile}",
         "-Dorg.gradle.testkit.dir=${project.rootProject.layout.projectDirectory.dir("intTestHomeDir/$distributionName").asFile}"]
    }
}
