
/*
 * Copyright 2010 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
import org.apache.tools.ant.filters.ReplaceTokens
import org.gradle.build.docs.CacheableAsciidoctorTask
import org.gradle.build.docs.Docbook2Xhtml
import org.gradle.build.docs.UserGuideTransformTask
import org.gradle.build.docs.dsl.docbook.AssembleDslDocTask
import org.gradle.build.docs.dsl.source.ExtractDslMetaDataTask
import org.gradle.build.docs.dsl.source.GenerateDefaultImportsTask
import org.gradle.build.docs.releasenotes.*
import org.gradle.build.docs.releasenotes.checks.*
import org.gradle.gradlebuild.BuildEnvironment
import org.gradle.gradlebuild.ProjectGroups
import org.gradle.gradlebuild.PublicApi
import org.gradle.gradlebuild.docs.RenderMarkdownTask
import org.gradle.gradlebuild.unittestandcompile.ModuleType

plugins {
    id 'base'
    id 'gradlebuild.jsoup'
    id 'javascript-base'
    id 'org.asciidoctor.convert'
}

repositories {
    javaScript.googleApis()
}

configurations {
    userGuideStyleSheets
    userGuideTask
    jquery
    jqueryTipTip

    cssElements {
        // define an outgoing configuration, aimed at consumers looking
        // for CSS resources
        attributes {
            attribute(Usage.USAGE_ATTRIBUTE, project.objects.named(Usage, 'css-resources'))
        }
        canBeResolved = false
        canBeConsumed = true
    }

    gradleSamplesElements {
        visible = false
        canBeResolved = false
        canBeConsumed = true
        attributes.attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage, "docs"))
        attributes.attribute(Attribute.of("type", String), "samples")
    }
    gradleFullDocsElements {
        visible = false
        canBeResolved = false
        canBeConsumed = true
        attributes.attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage, "docs"))
        attributes.attribute(Attribute.of("type", String), "full-docs")
    }
    gradleGettingStartedElements {
        visible = false
        canBeResolved = false
        canBeConsumed = true
        attributes.attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage, "docs"))
        attributes.attribute(Attribute.of("type", String), "getting-started")
    }

    gradleApiRuntime {
        visible = false
        canBeResolved = true
        canBeConsumed = false
        attributes.attribute(Usage.USAGE_ATTRIBUTE, project.objects.named(Usage, Usage.JAVA_RUNTIME))
        attributes.attribute(Attribute.of("org.gradle.api", String), "runtime")
    }
}

dependencies {
    asciidoctor "org.gradle:docs-asciidoctor-extensions:0.4.0"
    asciidoctor project.files("src/main/resources")

    userGuideTask 'xalan:xalan:2.7.1', 'xerces:xercesImpl:2.11.0'
    userGuideTask 'xhtmlrenderer:xhtmlrenderer:R8rc1'
    userGuideTask 'itext:itext:2.0.8'
    userGuideTask 'xslthl:xslthl:2.0.1'

    userGuideStyleSheets 'docbook:docbook-xsl:1.75.2@zip'
    jquery "jquery:jquery.min:1.8.0@js"
    jqueryTipTip "com.drewwilson.code:jquery.tipTip:1.3:minified@js"

    testImplementation project(":baseServices")
    testImplementation project(":core")
    testImplementation testLibraries.jsoup
    testImplementation("org.gebish:geb-spock:2.2")
    testImplementation('org.seleniumhq.selenium:selenium-htmlunit-driver:2.42.2')
    testImplementation libraries.commons_httpclient.coordinates
    testImplementation "org.apache.httpcomponents:httpmime:${libraries.commons_httpclient.version}"

    gradleApiRuntime project(":")
}

gradlebuildJava {
    moduleType = ModuleType.CORE
}

ext {
    srcDocsDir = file('src/docs')
    releaseFeaturesFile = file("$srcDocsDir/release/release-features.txt")
    userguideSrcDir = new File(srcDocsDir, 'userguide')
    userguideIntermediateOutputDir = new File(buildDir, 'userguideIntermediate')
    userguideSinglePageOutputDir = new File(buildDir, 'userguideSinglePage')
    dslSrcDir = new File(srcDocsDir, 'dsl')
    docsDir = file("$buildDir/docs")
    userguideDir = new File(docsDir, 'userguide')
    distDocsDir = new File(buildDir, 'distDocs')
    samplesDir = file("$buildDir/samples")
    docbookSrc = new File(project.buildDir, 'src')
    samplesSrcDir = file('src/samples')
}

configurations {
    gradleSamplesElements {
        outgoing.artifact(samplesDir) {
            builtBy 'samples'
        }
    }
    gradleFullDocsElements {
        outgoing.artifact(docsDir) {
            builtBy 'javadocAll', 'userguide', 'dslHtml', 'releaseNotes'
        }
    }
    gradleGettingStartedElements {
        outgoing.artifact(distDocsDir) {
            builtBy 'distDocs'
        }
    }
}

def configureCss = tasks.register("configureCss") {
    doLast {
        def tokens = fileTree(dir: "src/docs/css/images", include: "*.*").collectEntries {
            [it.name, it.bytes.encodeBase64().toString()]
        }

        css.inputs.property 'tokens', tokens
        css.filter ReplaceTokens, tokens: tokens
    }
}

def css = tasks.register("css", Sync) {
    dependsOn configureCss
    into "$buildDir/css"
    from "src/docs/css"
    include "*.css"
    include "*.svg"
}

artifacts {
    // declare that the generated css directory is an output of cssElements
    cssElements file: css.get().destinationDir, builtBy: css
}

// TODO This is a hack pending a solution to https://github.com/gradle/gradle/issues/4612
// Don't publish the CSS artifacts when calling uploadArchives
afterEvaluate {
    def notCssElements = configurations.archives.artifacts.findAll { !(it in configurations.cssElements.artifacts) }
    configurations.archives.artifacts.clear()
    artifacts {
        archives notCssElements
    }
}

ext.cssFiles = fileTree(css.map { it.destinationDir }) {
    builtBy css
}

def imageFiles = fileTree(userguideSrcDir) {
    include 'img/*.png'
    include 'img/*.gif'
    include 'img/*.jpg'
}
def resourceFiles = imageFiles + cssFiles

def samples = tasks.register("samples", Sync) {
    from samplesSrcDir
    into samplesDir

    exclude '**/readme.xml'
    exclude '**/build/**'
    exclude '**/.gradle/**'
}

tasks.register("userguideStyleSheets", Sync) {
    File stylesheetsDir = new File(srcDocsDir, 'stylesheets')
    into new File(buildDir, 'stylesheets')
    from(stylesheetsDir) {
        include '**/*.xml'
        include '*.xsl'
    }
    from(cssFiles)
    from({ zipTree(configurations.userGuideStyleSheets.singleFile) }) {
        // Remove the prefix
        eachFile { fcd -> fcd.path = fcd.path.replaceFirst('^docbook-xsl-[0-9\\.]+/', '') }
    }
}

def generatedResourcesDir = gradlebuildJava.generatedResourcesDir

def dslMetaData = tasks.register("dslMetaData", ExtractDslMetaDataTask) {
    source { javadocAll.source }
    destFile = new File(docbookSrc, 'dsl-meta-data.bin')
}

def dslDocbook = tasks.register("dslDocbook", AssembleDslDocTask) {
    dependsOn dslMetaData
    sources = fileTree(dir: dslSrcDir, includes: ['*.xml'])
    sourceFile = new File(dslSrcDir, 'dsl.xml')
    classDocbookDir = dslSrcDir
    classMetaDataFile = dslMetaData.get().destFile
    pluginsMetaDataFile = new File(dslSrcDir, 'plugins.xml')
    destFile = new File(docbookSrc, 'dsl.xml')
    linksFile = new File(docbookSrc, 'api-links.bin')
}

def defaultImports = tasks.register("defaultImports", GenerateDefaultImportsTask) {
    dependsOn dslMetaData
    metaDataFile = dslMetaData.get().destFile
    importsDestFile = new File(generatedResourcesDir, "default-imports.txt")
    mappingDestFile = new File(generatedResourcesDir, "api-mapping.txt")
    // These are part of the API, but not the DSL
    excludePackage 'org.gradle.tooling.**'
    excludePackage 'org.gradle.testfixtures.**'

    // Tweak the imports due to some inconsistencies introduced before we automated the default-imports generation
    excludePackage 'org.gradle.plugins.ide.eclipse.model'
    excludePackage 'org.gradle.plugins.ide.idea.model'
    excludePackage 'org.gradle.api.tasks.testing.logging'

    // TODO - rename some incubating types to remove collisions and then remove these exclusions
    excludePackage 'org.gradle.plugins.binaries.model'

    // Exclude classes that were moved in a different package but the deprecated ones are not removed yet
    excludePackage 'org.gradle.platform.base.test'
}

def dslStandaloneDocbook = tasks.register("dslStandaloneDocbook", UserGuideTransformTask) {
    dependsOn dslDocbook
    sourceFile = dslDocbook.get().destFile
    destFile = new File(docbookSrc, 'dsl-standalone.xml')
    dsldocUrl = '.'
    linksFile = new File(docbookSrc, 'api-links.bin')
    javadocUrl = '../javadoc'
    dsldocUrl = '../dsl'
    websiteUrl = 'https://gradle.org'
}

def dslHtml = tasks.register("dslHtml", Docbook2Xhtml) {
    group = "Documentation"
    dependsOn userguideStyleSheets
    source dslStandaloneDocbook
    destDir = new File(docsDir, 'dsl')
    classpath = configurations.userGuideTask
    stylesheetsDir = userguideStyleSheets.destinationDir
    stylesheetName = 'dslHtml.xsl'
    resources = cssFiles + fileTree(dslSrcDir) {
        include '*.js'
    }
    ext.entryPoint = "$destDir/index.html"
}

def userguideSinglePage = tasks.register("userguideSinglePage", CacheableAsciidoctorTask) {
    sourceDir = userguideSrcDir
    sources { include 'userguide_single.adoc' }
    outputDir = userguideSinglePageOutputDir
    backends = ['pdf', 'html5']

    attributes toc          : 'macro',
        toclevels           : 2,
        groovyDslPath       : "https://docs.gradle.org/${version}/dsl",
        javadocPath         : "https://docs.gradle.org/${version}/javadoc",
        kotlinDslPath       : "https://gradle.github.io/kotlin-dsl-docs/api",
        'source-highlighter': 'coderay'
}

def javaApiUrl = "https://docs.oracle.com/javase/8/docs/api"
def groovyApiUrl = "http://docs.groovy-lang.org/docs/groovy-${groovyVersion}/html/gapi"
def mavenApiUrl = "http://maven.apache.org/ref/${libraries.maven3.version}/maven-model/apidocs"

def javadocAll = tasks.register("javadocAll", Javadoc) {
    ext.stylesheetFile = file("src/docs/css/javadoc.css")
    inputs.file stylesheetFile withPropertyName "stylesheetFile" withPathSensitivity PathSensitivity.NAME_ONLY

    group = 'documentation'
    options.encoding = 'utf-8'
    options.docEncoding = 'utf-8'
    options.charSet = 'utf-8'
    options.addStringOption 'Xdoclint:syntax,html,reference', '-quiet'
    options.addStringOption "stylesheetfile", stylesheetFile.absolutePath
    options.addStringOption "source", "8"
    source ProjectGroups.INSTANCE.getPublicJavaProjects(project).collect { project -> project.sourceSets.main.allJava }
    destinationDir = new File(docsDir, 'javadoc') 
    classpath = configurations.gradleApiRuntime

    PublicApi.includes.each {
        include it
    }
    PublicApi.excludes.each {
        exclude it
    }
    options.links(javaApiUrl, groovyApiUrl, mavenApiUrl)
    title = "Gradle API $version"
    ext.entryPoint = "$destinationDir/index.html"

    if (BuildEnvironment.javaVersion.isJava11Compatible()) {
        options.addBooleanOption('html4', true)
        options.addBooleanOption('-no-module-directories', true)
        doLast {
            // This is a work-around for https://bugs.openjdk.java.net/browse/JDK-8211194. Can be removed once that issue is fixed on JDK's side
            // Since JDK 11, package-list is missing from javadoc output files and superseded by element-list file, but a lot of external tools still need it
            // Here we generate this file manually
            new File(destinationDir, 'package-list').text = new File(destinationDir, 'element-list').text

            // Commit http://hg.openjdk.java.net/jdk/jdk/rev/89dc31d7572b broke use of JSZip (https://bugs.openjdk.java.net/browse/JDK-8214856) fixed in Java 12 by http://hg.openjdk.java.net/jdk/jdk/rev/b4982a22926b
            // TODO: Remove this script.js workaround when we distribute Gradle using JDK 12 or higher
            project.copy { 
            	from(new File(srcDocsDir, "js/javadoc"))
                into(destinationDir)
            }
        }
    }
}

tasks.register("checkstyleApi", Checkstyle) {
    source javadocAll.get().source
    configFile = new File(checkstyle.configDir, "checkstyle-api.xml")
    classpath = files()
    reports.xml.destination = file("$checkstyle.reportsDir/checkstyle-api.xml")
}

def distDocs = tasks.register("distDocs", CacheableAsciidoctorTask) {
    sourceDir = userguideSrcDir
    outputDir = distDocsDir
    sources { include 'getting-started.adoc' }
    backends = ['html5']
}

asciidoctorj {
    version = '1.5.8.1'
    noDefaultRepositories = true
}

tasks.withType(CacheableAsciidoctorTask).configureEach {
    dependsOn css, samples, defaultImports

    separateOutputDirs = false
    options doctype: 'book'

    inputs.file("$srcDocsDir/css/manual.css").withPathSensitivity(PathSensitivity.RELATIVE)
    inputs.dir(file("src/main/resources")).withPathSensitivity(PathSensitivity.RELATIVE)
    inputs.dir(samplesSrcDir).withPathSensitivity(PathSensitivity.RELATIVE)

    attributes stylesdir           : '../css/',
        stylesheet          : 'manual.css',
        imagesdir           : 'img',
        nofooter            : true,
        sectanchors         : true,
        sectlinks           : true,
        linkattrs           : true,
        reproducible        : '',
        lang                : 'en-US',
        encoding            : 'utf-8',
        idprefix            : '',
        website             : 'https://gradle.org',
        javaApi             : javaApiUrl,
        jdkDownloadUrl      : 'http://jdk.java.net/',
        javadocReferenceUrl : 'http://docs.oracle.com/javase/8/docs/technotes/tools/windows/javadoc.html',
        minJdkVersion       : '8',
        antManual           : 'https://ant.apache.org/manual',
        docsUrl             : 'https://docs.gradle.org',
        guidesUrl           : 'https://guides.gradle.org',
        gradleVersion       : version,
        samplesPath         : "../../samples",
        'samples-dir'       : 'src/samples'
}

def userguideMultiPage = tasks.register("userguideMultiPage", CacheableAsciidoctorTask) {
    sourceDir = userguideSrcDir

    sources {
        include '*.adoc'
        exclude 'javaProject*Layout.adoc'
        exclude 'userguide_single.adoc'
    }
    outputDir = userguideIntermediateOutputDir

    backends = ['html5']

    attributes icons        : null,
        'note-caption'      : '&#10024;',
        'tip-caption'       : '&#128161;',
        'important-caption' : '&#10071;️',
        'caution-caption'   : '&#9888;',
        'warning-caption'   : '&#9888;',
        'source-highlighter': 'prettify',
        toc                 : 'auto',
        toclevels           : 1,
        'toc-title'         : 'Contents',
        groovyDslPath       : "../dsl",
        javadocPath         : "../javadoc",
        kotlinDslPath       : "https://gradle.github.io/kotlin-dsl-docs/api"
}

// Avoid overlapping outputs by copying exactly what we want from other intermediate tasks
def userguide = tasks.register("userguide", Sync) {
    dependsOn userguideMultiPage, userguideSinglePage
    description = 'Generates the userguide HTML and PDF'
    group = 'documentation'

    from resourceFiles
    from userguideIntermediateOutputDir
    from userguideSinglePageOutputDir

    into userguideDir
    rename 'userguide_single.pdf', 'userguide.pdf'
}

tasks.register("serveDocs", Exec) {
    description = 'Runs webserver to serve generate userguide docs'
    group = 'documentation'

    def webserverPort = 8000
    workingDir docsDir
    executable "python"
    args "-m", "SimpleHTTPServer", webserverPort

    dependsOn userguide

    doFirst {
        logger.lifecycle("ctrl+C to restart, serving Gradle docs at http://localhost:${webserverPort}")
    }
}

tasks.register("editReleaseNotes") {
    group = "release notes"
    doLast {
        Class.forName("java.awt.Desktop").getDesktop().edit(file("src/docs/release/notes.md"))
    }
}

def releaseNotesMarkdown = tasks.register("releaseNotesMarkdown", RenderMarkdownTask, file("src/docs/release/notes.md"), new File(buildDir, "release-notes-raw/release-notes.html"))
releaseNotesMarkdown.configure {
    group = "release notes"
}

def releaseNotes = tasks.register("releaseNotes", Copy) {
    group = "release notes"
    ext.fileName = "release-notes.html"
    into "$docsDir"
    from releaseNotesMarkdown
    jsoup.plugins "src/transforms/release-notes.gradle"
    filter(ReplaceTokens, tokens: [version: project.version.toString(), baseVersion: rootProject.baseVersion])
    ext.entryPoint = file("$docsDir/$fileName")
}

tasks.addRule("view«Doc Task Name» - Opens entry point") { String taskName ->
    if (taskName.startsWith("view")) {
        def realTaskName = (taskName - "view")
        realTaskName = realTaskName[0].toLowerCase() + realTaskName[1..-1]
        def task = tasks.findByName(realTaskName)
        if (task && task.hasProperty("entryPoint")) {
            tasks.create(taskName) {
                dependsOn task
                doLast {
                    Class.forName("java.awt.Desktop").getDesktop().browse(file(task.entryPoint).toURI())
                }
            }
        }
    }
}

def copyReleaseFeatures = tasks.register("copyReleaseFeatures", Copy) {
    from releaseFeaturesFile
    into generatedResourcesDir
}

sourceSets.main.output.dir generatedResourcesDir, builtBy: [defaultImports, copyReleaseFeatures]

tasks.named("test").configure {
    dependsOn releaseNotes
    inputs.files releaseNotesMarkdown.get().markdownFile withPropertyName "releaseNotesSource" withPathSensitivity PathSensitivity.NONE
    inputs.dir releaseNotes.get().destinationDir withPropertyName "releaseNotesRendered" withPathSensitivity PathSensitivity.NONE
    inputs.property "systemProperties", [:]
    systemProperty "org.gradle.docs.releasenotes.source", releaseNotesMarkdown.get().markdownFile
    systemProperty "org.gradle.docs.releasenotes.rendered", new File(releaseNotes.get().destinationDir, releaseNotes.get().fileName)
    systemProperty "org.gradle.docs.releasefeatures", releaseFeaturesFile
}

tasks.register("docs") {
    dependsOn javadocAll, userguide, distDocs, dslHtml, releaseNotes
    description = 'Generates all documentation'
    group = 'documentation'
}

tasks.register("docsZip", Zip) {
    // TODO: Reduce duplication
    from(docsDir)
    dependsOn 'javadocAll', 'userguide', 'dslHtml', 'releaseNotes'
}
