import org.gradle.build.JarJarJar

dependencies {
    compile project(':toolingApi')
    runtime project(':native')
    testCompile libraries.groovy
}

strictCompile()
useTestFixtures()

configurations.runtime {
    artifacts.clear()
}

tasks.remove(jar)

task jar(type: JarJarJar) {
    ext.shadedPackage = 'org.gradle.testkit.jarjar'
    from sourceSets.main.output
    from { configurations.runtime.collect { zipTree(it) } }

    exclude 'META-INF/**'
    exclude 'gradle-plugins.properties'
    exclude { details ->
        details.file.name.endsWith('-classpath.properties') &&
        details.file.name != 'gradle-test-kit-classpath.properties'
    }
    exclude 'net/rubygrapefruit/platform/**'
    exclude 'com/sun/jna/**'
    exclude 'org/fusesource/jansi/**'

    rule('org.gradle.testkit.**', '@0')
    rule('org.gradle.tooling.**', '@0')
    rule('org.gradle.api.Nullable', '@0')
    rule('org.gradle.api.Incubating', '@0')
    rule('org.gradle.**', "${shadedPackage}.@0")

    rule('org.**', "${shadedPackage}.@0")
    rule('com.**', "${shadedPackage}.@0")
    rule('net.**', "${shadedPackage}.@0")

    keep('org.gradle.testkit.**')
}

artifacts {
    runtime jar
}
