/*
 * Copyright 2016 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.gradle.testing.performance.generator.tasks.*

// === Gradle Build ===
tasks.register("gradleBuildCurrent", RemoteProject) {
    dependsOn rootProject.determineCommitId
    remoteUri = rootDir.absolutePath
    ref = provider { rootProject.determineCommitId.commitId }
}

tasks.register("gradleBuildBaseline", RemoteProject) {
    remoteUri = rootDir.absolutePath
    // Remember to update accordingly when rebasing/squashing
    // Do not use the "Rebase and merge" nor "Squash and merge" Github buttons when merging a PR that change the baseline
    ref = '25ed84ebe71581b7be37664a33f8670d11c8c711'
}

// === Java ===
['largeMonolithicJavaProject', 'largeJavaMultiProject', 'largeJavaMultiProjectKotlinDsl',
 'mediumMonolithicJavaProject', 'mediumJavaMultiProject', 'mediumJavaMultiProjectWithTestNG', 'mediumJavaCompositeBuild', 'mediumJavaPredefinedCompositeBuild',
 'smallJavaMultiProject'].each { template ->
    tasks.register(template, JavaExecProjectGeneratorTask) {
        outputs.dir new File(buildDir, template)
        outputs.cacheIf { true }
        inputs.property('template', template)
        inputs.property('args') { // Arguments are covered by the testProjectName and the outputs. We don't want them to contain the absolute path
            return []
        }
        classpath = sourceSets.performanceTest.runtimeClasspath
        main = 'org.gradle.performance.generator.TestProjectGenerator'
        args name, buildDir.absolutePath
    }
}

// TODO implement VerboseTestOutputPerformanceTest with static project
tasks.register("withVerboseTestNG", JvmProjectGeneratorTask) {
    projects = 1
    sourceFiles = 10
    subProjectTemplates << 'with-verbose-testng'
}
tasks.register("withVerboseJUnit", JvmProjectGeneratorTask) {
    projects = 1
    sourceFiles = 10
    subProjectTemplates << 'with-verbose-junit'
}

//TODO replace with direct task objects creation in test
tasks.register("createLotsOfTasks", JvmProjectGeneratorTask) {
    projects = 1
    sourceFiles = 0
    subProjectTemplates = ['task-creation']
    templateArgs = [
        taskCount: 15000
    ]
    dependencyGraph {
        size = 0
    }

    numberOfExternalDependencies = 0
}

tasks.register("generateLotsOfDeprecationWarnings", Copy) {
    into "build/$name"
    from "src/templates/$name"
}

tasks.register("largeJavaBuildBuilder", BuildBuilderGenerator) {
    projectType = "java"
    projects = 100
    sourceFiles = 4
    description = "Generates a ${projectType} build using build-builder (${projects} projects, ${sourceFiles} files)"
}




// === Gradle Kotlin DSL ===
tasks.register("ktsManyProjects", KtsProjectGeneratorTask) {
    projects = 100
    sourceFiles = 0
}

tasks.register("ktsSmall", KtsProjectGeneratorTask) {
}

// === Native Software Model ===
tasks.register("smallNative", NativeProjectGeneratorTask) {
    projects = 1
    sourceFiles = 20
    templateArgs = [
        moduleCount: 1,
        functionCount: 1
    ]
    subProjectTemplates = ['native-source', 'native-component']
}

tasks.register("mediumNative", NativeProjectGeneratorTask) {
    projects = 1
    sourceFiles = 100
    templateArgs = [
        moduleCount: 1,
        functionCount: 20
    ]
    subProjectTemplates = ['native-source', 'native-component']
}

tasks.register("bigNative", NativeProjectGeneratorTask) {
    projects = 1
    sourceFiles = 500
    templateArgs = [
        moduleCount: 1,
        functionCount: 50
    ]
    subProjectTemplates = ['native-source', 'native-component']
}

tasks.register("multiNative", NativeProjectGeneratorTask) {
    projects = 10
    sourceFiles = 20
    templateArgs = [
        moduleCount: 10,
        functionCount: 20
    ]
    subProjectTemplates = ['native-source', 'native-component']
}

tasks.register("manyProjectsNative", NativeProjectGeneratorTask) {
    projects = 100
    sourceFiles = 1
    templateArgs = [
        moduleCount: 3,
        functionCount: 1
    ]
    subProjectTemplates = ['native-source', 'native-component']
}

tasks.register("smallPCHNative", NativeProjectGeneratorTask) {
    projects = 1
    sourceFiles = 20
    templateArgs = [
        moduleCount: 1,
        functionCount: 1
    ]
    subProjectTemplates = ['native-pch-source', 'native-pch-component']
}

tasks.register("mediumPCHNative", NativeProjectGeneratorTask) {
    projects = 1
    sourceFiles = 200
    templateArgs = [
        moduleCount: 1,
        functionCount: 1
    ]
    subProjectTemplates = ['native-pch-source', 'native-pch-component']
}

tasks.register("bigPCHNative", NativeProjectGeneratorTask) {
    projects = 1
    sourceFiles = 1000
    templateArgs = [
        moduleCount: 1,
        functionCount: 1
    ]
    subProjectTemplates = ['native-pch-source', 'native-pch-component']
}

tasks.register("nativeMonolithicOverlapping", MonolithicNativeProjectGeneratorTask) {
    templateArgs = [overlapWithOutput: true]
    projects = 100
    // number of source files to generate in each project of each type
    // this generates 200 C, 200 C++, 200 headers and 400 unused C/C++ files
    sourceFiles = 200
}

tasks.register("nativeMonolithic", MonolithicNativeProjectGeneratorTask) {
    templateArgs = [overlapWithOutput: false]
    projects = 100
    sourceFiles = 200
}

tasks.register("smallNativeMonolithic", MonolithicNativeProjectGeneratorTask) {
    templateArgs = [overlapWithOutput: false]
    projects = 2
    sourceFiles = 100
}

tasks.register("mediumNativeMonolithic", MonolithicNativeProjectGeneratorTask) {
    templateArgs = [overlapWithOutput: false]
    projects = 10
    sourceFiles = 200
}

["nativeMonolithicOverlapping", "nativeMonolithic", "smallNativeMonolithic", "mediumNativeMonolithic"].collect { tasks.withType(MonolithicNativeProjectGeneratorTask).named(it) }*.configure {
    templateArgs += [
        // number of functions created in each source file
        functionCount: 50,
        // number of prebuilt libraries to generate
        prebuiltLibraries: 30,
        // number of files included in each source set
        includedSourceCount: 50,
        // number of headers each source file includes
        includedHeaderCount: 10,
        // number of 'common' headers each source file includes
        includedCommonHeaderCount: 10,
    ]
    rootProjectTemplates = ['native-monolithic']
    subProjectTemplates = []
    additionalProjectFiles = ['common.gradle', 'prebuilt.gradle', 'components.gradle']
}

tasks.register("nativeDependents", NativeProjectWithDepsGeneratorTask) {
    resources.from(fileTree(resolveTemplate("native-dependents-resources")))
    resources.from(resolveTemplate("init.gradle"))
}

tasks.register("nativeDependentsDeep", NativeProjectWithDepsGeneratorTask) {
    resources.from(fileTree(resolveTemplate("native-dependents-resources")))
    resources.from(resolveTemplate("init.gradle"))
    generateDeepHierarchy = true
}

// === Native Current Model Plugins ===
tasks.register("smallCppApp", CppMultiProjectGeneratorTask) {
    projects = 1
    sourceFiles = 20
    templateArgs = [
        functionCount: 2
    ]
    subProjectTemplates = ['cpp-source', 'cpp-project']
}

tasks.register("mediumCppApp", CppMultiProjectGeneratorTask) {
    projects = 1
    sourceFiles = 100
    templateArgs = [
        functionCount: 20
    ]
    subProjectTemplates = ['cpp-source', 'cpp-project']
}

tasks.register("mediumCppAppWithMacroIncludes", CppMultiProjectGeneratorTask) {
    projects = 1
    sourceFiles = 100
    templateArgs = [
        functionCount: 20,
        useMacroIncludes: true

    ]
    subProjectTemplates = ['cpp-source', 'cpp-project']
}

tasks.register("bigCppApp", CppMultiProjectGeneratorTask) {
    projects = 1
    sourceFiles = 500
    templateArgs = [
        functionCount: 50
    ]
    subProjectTemplates = ['cpp-source', 'cpp-project']
}

tasks.register("smallCppMulti", CppMultiProjectGeneratorTask) {
    projects = 11
    sourceFiles = 2
    templateArgs = [
        functionCount: 1
    ]
    subProjectTemplates = ['cpp-source', 'cpp-project']
}

tasks.register("mediumCppMulti", CppMultiProjectGeneratorTask) {
    projects = 101
    sourceFiles = 4
    templateArgs = [
        functionCount: 2
    ]
    subProjectTemplates = ['cpp-source', 'cpp-project']
}

tasks.register("mediumCppMultiWithMacroIncludes", CppMultiProjectGeneratorTask) {
    projects = 101
    sourceFiles = 4
    templateArgs = [
        functionCount: 2,
        useMacroIncludes: true
    ]
    subProjectTemplates = ['cpp-source', 'cpp-project']
}

tasks.register("bigCppMulti", CppMultiProjectGeneratorTask) {
    projects = 201
    sourceFiles = 8
    templateArgs = [
        functionCount: 3
    ]
    subProjectTemplates = ['cpp-source', 'cpp-project']
}

// Build-builder projects
tasks.register("buildBuilder", RemoteProject) {
    remoteUri = 'https://github.com/adammurdoch/build-builder.git'
    ref = 'ef909790b6d4311609af0cb53373b1c8a9180718'
}

tasks.register("installBuildBuilder", GradleBuild) {
    dir = buildBuilder.outputDirectory
    tasks = ['install']
    dependsOn buildBuilder
}

tasks.withType(BuildBuilderGenerator).configureEach {
    buildBuilderInstall = new File(tasks.buildBuilder.outputDirectory, "build/install/build-builder")
    dependsOn installBuildBuilder
}

tasks.register("mediumSwiftMulti", BuildBuilderGenerator) {
    projectType = "swift"
    projects = 100
    sourceFiles = 4
    description = "Generates a ${projectType} build using build-builder (${projects} projects, ${sourceFiles} files)"
}

tasks.register("bigSwiftApp", BuildBuilderGenerator) {
    projectType = "swift"
    projects = 1
    sourceFiles = 500
    description = "Generates a ${projectType} build using build-builder (${projects} projects, ${sourceFiles} files)"

    doLast {
        def buildFile = generatedDir.file("build.gradle").get().asFile
        buildFile << """
            tasks.withType(SwiftCompile) {
                compilerArgs.addAll provider {
                    [ "-num-threads", "6", "-j", "6" ]
                }
            }
        """
    }
}

// === Android ===
tasks.register("k9AndroidBuild", RemoteProject) {
    remoteUri = 'https://github.com/gradle/perf-android-k-9.git'
    branch = 'android-31'
}

tasks.register("largeAndroidBuild", RemoteProject) {
    remoteUri = 'https://github.com/gradle/perf-android-large.git'
    branch = 'android-35'
}

tasks.register("santaTrackerAndroidBuild", RemoteProject) {
    remoteUri = 'https://github.com/gradle/santa-tracker-android.git'
    branch = 'agp-3.5.0'
    doLast {
        new File(outputDirectory, 'santa-tracker/google-services.json').text = """
{
  "project_info": {
    "project_number": "012345678912",
    "firebase_url": "https://example.com",
    "project_id": "example",
    "storage_bucket": "example.example.com"
  },
  "client": [
    {
      "client_info": {
        "mobilesdk_app_id": "1:012345678912:android:0123456789abcdef",
        "android_client_info": {
          "package_name": "com.google.android.apps.santatracker.debug"
        }
      },
      "oauth_client": [
        {
          "client_id": "foo.example.com",
          "client_type": 3
        }
      ],
      "api_key": [
        {
          "current_key": "012345678901234567890123456789012345678"
        }
      ],
      "services": {
        "analytics_service": {
          "status": 1
        },
        "appinvite_service": {
          "status": 1,
          "other_platform_oauth_client": []
        },
        "ads_service": {
          "status": 2
        }
      }
    },
    {
      "client_info": {
        "mobilesdk_app_id": "1:012345678912:android:0123456789abcdef",
        "android_client_info": {
          "package_name": "com.google.android.apps.santatracker.debug"
        }
      },
      "oauth_client": [
        {
          "client_id": "foo.example.com",
          "client_type": 3
        }
      ],
      "api_key": [
        {
          "current_key": "012345678901234567890123456789012345678"
        }
      ],
      "services": {
        "analytics_service": {
          "status": 1
        },
        "appinvite_service": {
          "status": 1,
          "other_platform_oauth_client": []
        },
        "ads_service": {
          "status": 2
        }
      }
    }
  ],
  "configuration_version": "1"
}
"""
    }
}

tasks.register("excludeRuleMergingBuild", RemoteProject) {
    remoteUri = 'https://github.com/gradle/performance-comparisons.git'
    branch = 'master'
    subdirectory = 'exclude-merging'
}

tasks.register("springBootApp", RemoteProject) {
    remoteUri = 'https://github.com/gradle/performance-comparisons.git'
    branch = 'master'
    subdirectory = 'parallel-downloads'
}

tasks.register("largeNativeBuild", RemoteProject) {
    remoteUri = 'https://github.com/gradle/perf-native-large'
    branch = 'master'
    finalizedBy 'largeNativeBuildPrebuilt'
}

tasks.register("largeNativeBuildPrebuilt", GradleBuild) {
    buildFile = file("$largeNativeBuild.outputDirectory/prebuilt/util/build.gradle")
    dir = file("$largeNativeBuild.outputDirectory/prebuilt/util")
    tasks = ['build']
}

tasks.register("archivePerformanceProject", Copy) {
    into "build/$name"
    from "src/templates/$name"
}

tasks.register("workerApiProject", Copy) {
    into "build/$name"
    from "src/templates/$name"
}

tasks.withType(JvmProjectGeneratorTask).configureEach {
    if (project.hasProperty("springDmPluginVersion")) {
        templateArgs['springDmPluginVersion'] = springDmPluginVersion
    }
    templateArgs['projectDependencies'] = true
}

// The Android Studio performance tests use custom model classes which need to be found in
// a dedicated project in order for the classpath to be setup properly
def androidRemoteProjects = tasks.withType(RemoteProject).matching { it.name =~ /Android/ }
androidRemoteProjects.configureEach {
    dependsOn tasks.findByPath(':internalAndroidPerformanceTesting:buildClassPath')
    doLast {
        file("$outputDirectory/tapi-classpath.txt") << project(':internalAndroidPerformanceTesting').buildClassPath.outputFile.text
    }
}
