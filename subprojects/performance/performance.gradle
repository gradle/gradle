apply from: 'src/generator.groovy'

configurations {
    junit
}

dependencies {
    junit 'junit:junit:4.10'
    groovy libraries.groovy
    testFixturesCompile project(':internalIntegTesting')
}

useTestFixtures()

task small(type: ProjectGeneratorTask, description: 'Generates a small project') {
}

task largeSrc(type: ProjectGeneratorTask, description: 'Generates a single project with lots of source files') {
    sourceFiles = 50000
    linesOfCodePerSourceFile = 20
    withPlainAntCompile = true
}

task multi(type: ProjectGeneratorTask, description: 'Generates a multi-project build') {
    projects = 25
    sourceFiles = 100
}

task parallel(type: ProjectGeneratorTask, description: 'Generates a multi-project build') {
    projects = 8
    sourceFiles = 1000
}

task mixedSize(type: ProjectGeneratorTask) {
    projects = 400
    sourceFiles = 100
    projects[1].sourceFiles = 20000
}

task withTestNG(type: ProjectGeneratorTask) {
    projects = 2
    sourceFiles = 10
    testReport = false
    testFramework = 'useTestNG()'
    testClassTemplate = 'TestNGTest.java'
}

task withVerboseJUnits(type: ProjectGeneratorTask) {
    projects = 2
    sourceFiles = 10
    testReport = true
    testClassTemplate = 'VerboseJUnitTest.java'
}

task multiGroovy(type: ProjectGeneratorTask, description: 'Generates a multi-project groovy build') {
    projects = 25
    groovyProject = true
}

task multiScala(type: ProjectGeneratorTask, description: 'Generates a multi-project Scala build') {
    projects = 25
    scalaProject = true
}

task largeMulti(type: ProjectGeneratorTask, description: 'Generates a large multi-project build') {
    projects = 800
    sourceFiles = 100
}

task lotDependencies(type: ProjectGeneratorTask, description: 'Generates a small multi-project build with a large Dependency Graph'){
    projects = 5
    sourceFiles = 100

    dependencyGraph {
        size = 200
        depth = 4
        useSnapshotVersions = true //default is false
    }
}

def generators = tasks.withType(ProjectGeneratorTask)
generators.all {
    group = 'Project setup'
    testDependencies = configurations.junit
}
task all(dependsOn: generators)

task prepareSamples(dependsOn: [small, multi, lotDependencies, withTestNG, withVerboseJUnits])

integTestTasks.all {
    if (buildTypes.isActive('performanceTest')) {
        dependsOn prepareSamples
    } else {
        enabled = false
        dependsOn = []
    }
    maxParallelForks = 1
}

tasks.integTest.testLogging {
    showStandardStreams = true
    lifecycle {
        exceptionFormat 'full'
    }
}

eclipse {
    classpath {
        file.whenMerged { classpath ->
            //**TODO
            classpath.entries.removeAll {it.path.contains('src/test/groovy')}
            classpath.entries.removeAll {it.path.contains('src/testFixtures/groovy')}
            classpath.entries.removeAll {it.path.contains('src/integTest/groovy')}
        }
    }
}
