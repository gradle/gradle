import org.gradle.gradlebuild.unittestandcompile.ModuleType

/*
 * Copyright 2016 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'gradlebuild.classycle'
}

apply from: 'templates.gradle'

dependencies {
    // so that all Gradle features are available
    performanceTestRuntime allTestRuntimeDependencies

    testFixturesApi project(':internalPerformanceTesting')
}

gradlebuildJava {
    moduleType = ModuleType.INTERNAL
}

tasks.withType(org.gradle.testing.PerformanceTest).configureEach {
    resultStoreClass = "org.gradle.performance.results.BuildScanResultsStore"
    systemProperties += [incomingArtifactDir: "${rootDir}/incoming/"]
}

tasks.named('performanceReport').configure {
    doLast {
        project.delete("build/reports/performance")
        project.javaexec(new Action<JavaExecSpec>() {
            void execute(JavaExecSpec spec) {
                spec.setMain("org.gradle.performance.results.ReportGenerator")
                spec.args("org.gradle.performance.results.BuildScanResultsStore", "build/reports/performance")
                spec.systemProperties(performanceTest.databaseParameters)
                spec.systemProperty("org.gradle.performance.execution.channel", "commits")
                spec.setClasspath(performanceTest.classpath)
            }
        })
    }
}

tasks.withType(org.gradle.testing.PerformanceTest).configureEach{
    finalizedBy 'performanceReport'
}

tasks.named("performanceAdhocTest").configure { enabled = false }
