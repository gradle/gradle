/*
 * Copyright 2010 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply from: "$rootDir/gradle/providedConfiguration.gradle"

configurations {
    testFixtures
}

if (!javaVersion.java6Compatible) {
    sourceSets.main.groovy.exclude '**/jdk6/**'
}

dependencies {
    groovy libraries.groovy

    compile project(':baseServices')
    compile project(':core')
    compile project(':coreImpl')
    compile project(':messaging')
    compile project(':wrapper')
    compile project(':reporting')

    compile libraries.ant
    compile libraries.asm
    compile libraries.commons_io
    compile libraries.commons_lang
    compile libraries.junit
    compile libraries.slf4j_api
    compile 'org.testng:testng:6.3.1'

    provided files(jvm.toolsJar) // for SunJavaCompiler
    provided 'javax.inject:javax.inject:1'

    runtime libraries.commons_cli

    testCompile libraries.jsoup
}

evaluationDependsOn(":wrapper")
task wrapperJar(type: Copy) {
    from project(":wrapper").executableJar.outputs.files
    into generatedResourcesDir
}
sourceSets.main.output.dir generatedResourcesDir, builtBy: wrapperJar

test {
    exclude 'org/gradle/api/internal/tasks/testing/junit/ATestClass*.*'
    exclude 'org/gradle/api/internal/tasks/testing/junit/ABroken*TestClass*.*'
}

useTestFixtures()
useTestFixtures(sourceSet: "testFixtures")
useTestFixtures(project: ":coreImpl")
