apply plugin: "groovy"

import org.gradle.build.JarJar

configurations {
    mvn3Input
}

dependencies {
    groovy libraries.groovy

    compile project(":core")

    compile libraries.commons_httpclient
    compile libraries.commons_lang
    compile libraries.commons_io
    compile libraries.ivy
    compile libraries.slf4j_api
    compile libraries.maven_ant_tasks
    compile libraries.nekohtml

    testCompile libraries.junit

    compile files(["$buildDir/libs/jarjar/jarjar-maven-settings-3.0.4.jar",
            "$buildDir/libs/jarjar/jarjar-maven-settings-builder-3.0.4.jar",
            "$buildDir/libs/jarjar/jarjar-plexus-cipher-1.7.jar",
            "$buildDir/libs/jarjar/jarjar-plexus-component-annotations-1.5.5.jar",
            "$buildDir/libs/jarjar/jarjar-plexus-interpolation-1.14.jar",
            "$buildDir/libs/jarjar/jarjar-plexus-sec-dispatcher-1.3.jar",
            "$buildDir/libs/jarjar/jarjar-plexus-utils-2.0.6.jar"]) {
        builtBy tasks.withType(JarJar)
    }

    mvn3Input libraries.maven3_settings_builder
}
//TODO SF we should not resolve at config time
configurations.mvn3Input.files.each{libFile->
   task "jarjar-${libFile.name}"(type: JarJar) {
        inputFile = libFile
        outputFile = file("$buildDir/libs/jarjar/jarjar-${libFile.name}")
        rule('org.**', 'jarjar.org.@1')

       //TODO SF
       //org.apache.maven -> org.gradle.mvn3.org.apache.maven
   }
}

def allJarJars = tasks.withType(JarJar)
ideaModule.dependsOn allJarJars // I expected that buildable file collections links the ideaModule to the allJarJars already.
eclipseClasspath.dependsOn allJarJars 
useTestFixtures()