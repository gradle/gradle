/*
 * Copyright 2010 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
import java.text.DateFormat

apply id: 'groovy'

configurations {
    testFixtures
    testFixturesRuntime {
        extendsFrom testFixtures, testRuntime
    }
    integTestCompile {
        extendsFrom testCompile
    }
    integTestRuntime {
        extendsFrom intTestCompile, testRuntime
    }
    integTestFixtures {
        extendsFrom testFixtures
    }
    integTestFixturesRuntime {
        extendsFrom integTestFixtures, integTestRuntime
    }
}

sourceSets {
    integTest {
        compileClasspath = sourceSets.test.classes + sourceSets.main.classes + configurations.integTestCompile
        runtimeClasspath = classes + compileClasspath + configurations.integTestRuntime
    }
}

dependencies {
    groovy libraries.groovy_depends

    compile "commons-httpclient:commons-httpclient:3.0@jar", "commons-codec:commons-codec:1.2@jar", libraries.jcl_to_slf4j

    compile libraries.ivy, "jsch:jsch:0.1.31@jar"

    compile libraries.jopt_simple,
            libraries.ant,
            "org.apache.ant:ant-nodeps:1.7.0@jar",
            libraries.logback_classic,
            libraries.logback_core,
            libraries.slf4j_api,
            libraries.jul_to_slf4j,
            libraries.commons_io,
            libraries.commons_lang,
            "commons-codec:commons-codec:1.2@jar",
            "commons-collections:commons-collections:3.2.1@jar",
            "slide:webdavlib:2.0@jar",
            "org.apache.maven:maven-ant-tasks:2.1.0@jar",
            libraries.asm_all

    runtime libraries.log4j_to_slf4j, libraries.jcl_to_slf4j

    testCompile "xmlunit:xmlunit:1.2"

    runtime libraries.ant_launcher,
            "org.apache.ant:ant-trax:1.7.0@jar"

    integTestCompile libraries.jetty_depends

    testFixtures sourceSets.test.classes, sourceSets.main.classes
    integTestFixtures sourceSets.integTest.classes
}

task versionProperties(type: WriteVersionProperties) {
    propertiesFile = new File(sourceSets.main.classesDir, GradleVersion.FILE_NAME)
}

classes.dependsOn(versionProperties)

task ideVersionProperties(type: WriteVersionProperties) {
    propertiesFile = new File(ideDir, "resources/test/$GradleVersion.FILE_NAME")
}

ide.dependsOn ideVersionProperties

[compileGroovy, compileTestGroovy]*.groovyOptions*.fork(memoryInitialSize: '128M', memoryMaximumSize: '1G')

test {
    xmsSize = '128m'
    xmxSize = '512m'
    maxPermSize = '128m'

    // there must be a nicer way of doing this...
    try { xmsSize = customXmsSize; logger.debug('Using custom Xms size: ' + xmsSize + ' for :core:test') } catch (MissingPropertyException e) { }
    try { xmxSize = customXmxSize; logger.debug('Using custom Xmx size: ' + xmxSize + ' for :core:test') } catch (MissingPropertyException e) { }
    try { maxPermSize = customMaxPermSize; logger.debug('Using custom MaxPermSize: ' + maxPermSize + ' for :core:test') } catch (MissingPropertyException e) { }

    options.fork(jvmArgs: ["-ea", "-Xms${xmsSize}", "-Xmx${xmxSize}", "-XX:MaxPermSize=${maxPermSize}", '-XX:+HeapDumpOnOutOfMemoryError'])
}

class WriteVersionProperties extends DefaultTask {
    @Input
    String getVersion() { return project.version.toString() }

    @Input
    Date getBuildTime() { return project.version.buildTime }

    @OutputFile
    File propertiesFile

    @TaskAction
    def void generate() {
        logger.info('Write version properties to: {}', propertiesFile)
        Properties versionProperties = new Properties()
        versionProperties.putAll([
                (GradleVersion.VERSION): version,
                (GradleVersion.BUILD_TIME): DateFormat.getDateTimeInstance(DateFormat.FULL, DateFormat.FULL).format(buildTime)
        ])
        propertiesFile.withOutputStream {
            versionProperties.store(it, '')
        }

    }
}
