import gradlebuild.integrationtests.model.GradleDistribution
import org.asciidoctor.gradle.jvm.AsciidoctorTask
import org.gradle.docs.internal.tasks.CheckLinks
import org.gradle.docs.samples.internal.tasks.InstallSample
import org.gradle.internal.os.OperatingSystem

import javax.inject.Inject

import static gradlebuild.basics.BuildEnvironmentKt.repoRoot
import static gradlebuild.basics.BuildParamsKt.isConfigurationCacheEnabledForDocsTests
import static gradlebuild.basics.BuildParamsKt.shouldRunBrokenForConfigurationCacheDocsTests
import static gradlebuild.basics.Repositories_extensionsKt.googleApisJs

plugins {
    id 'gradlebuild.internal.java'
    // TODO: Apply asciidoctor in documentation plugin instead.
    id 'org.asciidoctor.jvm.convert'
    id 'gradlebuild.documentation'
    id 'gradlebuild.generate-samples'
    id 'gradlebuild.split-docs'
}

repositories { handler ->
    googleApisJs(handler)
}

configurations {
    gradleFullDocsElements {
        attributes {
            attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage, Usage.JAVA_RUNTIME))
            attribute(Category.CATEGORY_ATTRIBUTE, objects.named(Category, Category.DOCUMENTATION))
            attribute(DocsType.DOCS_TYPE_ATTRIBUTE, objects.named(DocsType, "gradle-documentation"))
        }
        visible = false
        canBeResolved = false
        canBeConsumed = true
    }
    docsTestRuntimeClasspath.extendsFrom(integTestDistributionRuntimeOnly)
}

configurations.docsTestImplementation {
    // The 'gradlebuild.generate-samples' plugin uses the 'org.gradle.samples' plugin from the old gradle/guides build, which pulls in slf4j-simple, which we don't want.
    // Because this is done directly by the plugin application logic, we can't use a ComponentMetadataRule to exclude it.
    // See: https://github.com/gradle/guides/blob/ba018cec535d90f75876bfcca29381d213a956cc/subprojects/gradle-guides-plugin/src/main/java/org/gradle/docs/samples/internal/SamplesDocumentationPlugin.java#L335
    exclude([group: "org.slf4j", module: "slf4j-simple"])
}

dependencies {
    // generate Javadoc for the full Gradle distribution
    runtimeOnly project(":distributions-full")

    userGuideTask 'xalan:xalan:2.7.1'
    userGuideTask 'xerces:xercesImpl:2.11.0'
    userGuideTask 'net.sf.xslthl:xslthl:2.0.1'

    userGuideStyleSheets 'net.sf.docbook:docbook-xsl:1.75.2:resources@zip'

    jquery "jquery:jquery.min:3.5.1@js"

    testImplementation project(":base-services")
    testImplementation project(":core")
    testImplementation libs.jsoup
    testImplementation "org.gebish:geb-spock:2.2"
    testImplementation 'org.seleniumhq.selenium:selenium-htmlunit-driver:2.42.2'
    testImplementation libs.commonsHttpclient
    testImplementation libs.httpmime

    docsTestImplementation platform(project(":distributions-dependencies"))
    docsTestImplementation project(":internal-integ-testing")
    docsTestImplementation project(":base-services")
    docsTestImplementation project(":logging")
    docsTestImplementation libs.junit5Vintage
    docsTestImplementation libs.junit

    integTestDistributionRuntimeOnly project(":distributions-full")
}

asciidoctorj {
    version = '2.5.11'
    modules.pdf.version '2.3.10'
}

tasks.withType(AsciidoctorTask).configureEach { task ->
    if (task.name == "userguideSinglePagePdf") {
        task.asciidoctorj.docExtensions(
            project.getDependencies().create(project(":docs-asciidoctor-extensions-base")),
        )
    } else {
        task.asciidoctorj.docExtensions(
            project.getDependencies().create(project(":docs-asciidoctor-extensions")),
            project.getDependencies().create(project.files("src/main/resources"))
        )
    }
}

gradleDocumentation {
    javadocs {
        javaApi = project.uri("https://docs.oracle.com/javase/8/docs/api")
        groovyApi = project.uri("https://docs.groovy-lang.org/docs/groovy-${libs.groovyVersion}/html/gapi")
    }
    kotlinDslReference {
        dokkaVersionOverride = "1.9.10"
    }
}

samples {

    // TODO: Do this lazily so we don't need to walk the filesystem during configuration
    // iterate through each snippets and record their names and locations
    FileFilter directoriesOnly = { it.directory }
    def topLevelDirs = file('src/snippets').listFiles(directoriesOnly)
    def snippetDirs = topLevelDirs*.listFiles(directoriesOnly).flatten().grep {
        new File(it, "kotlin").exists() || new File(it, "groovy").exists()
    }

    snippetDirs.each { File snippetDir ->
        String snippetName = snippetDir.name
        String categoryName = snippetDir.parentFile.name
        def id = org.gradle.docs.internal.StringUtils.toLowerCamelCase("snippet-" + categoryName + "-" + snippetName)
        publishedSamples.create(id) {
            description = "Snippet from $snippetDir"
            category = "Other"
            readmeFile = file("src/snippets/default-readme.adoc")
            sampleDirectory = snippetDir
            promoted = false
        }
    }

}

// Use the version of Gradle being built, not the version of Gradle used to build,
// also don't validate distribution url, since it is just a local distribution
tasks.named("generateWrapperForSamples", Wrapper) {
    gradleVersion = project.version
    validateDistributionUrl = false
}

// TODO: The rich console to plain text is flaky
tasks.named("checkAsciidoctorSampleContents") {
    enabled = false
}

// exclude (unused and non-existing) wrapper of development Gradle version, as well as README, because the timestamp in the Gradle version break the cache
tasks.withType(InstallSample).configureEach {
    if (name.contains('ForTest')) {
        excludes.add("gradle/wrapper/**")
        excludes.add("README")
    }
}

tasks.named("quickTest") {
    dependsOn("checkDeadInternalLinks")
}

// TODO add some kind of test precondition support in sample test conf
tasks.named("docsTest") { task ->
    maxParallelForks = 2
    // The org.gradle.samples plugin uses Exemplar to execute integration tests on the samples.
    // Exemplar doesn't know about that it's running in the context of the gradle/gradle build
    // so it uses the Gradle distribution from the running build. This is not correct, because
    // we want to verify that the samples work with the Gradle distribution being built.
    def installationEnvProvider = objects.newInstance(GradleInstallationForTestEnvironmentProvider.class, project, task)
    installationEnvProvider.gradleHomeDir.from(configurations.integTestDistributionRuntimeClasspath)
    installationEnvProvider.samplesdir = project.layout.buildDirectory.dir("working/samples/testing")
    jvmArgumentProviders.add(installationEnvProvider)

    // For unknown reason, this is set to 'sourceSet.getRuntimeClasspath()' in the 'org.gradle.samples' plugin
    testClassesDirs = sourceSets.docsTest.output.classesDirs
    // 'integTest.samplesdir' is set to an absolute path by the 'org.gradle.samples' plugin
    systemProperties.clear()

    filter {
        // workaround for https://github.com/gradle/dotcom/issues/5958
        failOnNoMatchingTests = false
    }

    if (isConfigurationCacheEnabledForDocsTests(project)) {
        systemProperty("org.gradle.integtest.samples.cleanConfigurationCacheOutput", "true")
        systemProperty("org.gradle.integtest.samples.checkLoadingFromConfigurationCache", "true")
        systemProperty("org.gradle.integtest.executer", "configCache")

        filter {
            // Configuration cache samples enable configuration cache explicitly. We're not going to run them with the configuration cache executer.
            excludeTestsMatching "org.gradle.docs.samples.*.snippet-configuration-cache-*.sample"
            excludeTestsMatching "*WithoutCC*.sample"

            // These tests cover features that are not planned to be supported in the first stable release of the configuration cache.
            def testsForUnsupportedFeatures = [
                "snippet-ant-add-behaviour-to-ant-target_groovy_addBehaviourToAntTarget.sample",
                "snippet-ant-add-behaviour-to-ant-target_kotlin_addBehaviourToAntTarget.sample",
                "snippet-ant-depends-on-ant-target_groovy_dependsOnAntTarget.sample",
                "snippet-ant-depends-on-ant-target_kotlin_dependsOnAntTarget.sample",
                "snippet-ant-depends-on-task_groovy_dependsOnTask.sample",
                "snippet-ant-depends-on-task_kotlin_dependsOnTask.sample",
                "snippet-ant-hello_groovy_antHello.sample",
                "snippet-ant-hello_kotlin_antHello.sample",
                "snippet-ant-rename-task_groovy_renameAntDelegate.sample",
                "snippet-ant-rename-task_kotlin_renameAntDelegate.sample",
                "snippet-ant-use-external-ant-task-with-config_groovy_useExternalAntTaskWithConfig.sample",
                "snippet-ant-use-external-ant-task-with-config_kotlin_useExternalAntTaskWithConfig.sample",
                "snippet-ant-ant-logging_groovy_antLogging.sample",
                "snippet-ant-ant-logging_kotlin_antLogging.sample",
                "snippet-buildlifecycle-task-execution-events_groovy_sanityCheck.sample",
                "snippet-buildlifecycle-task-execution-events_groovy_taskExecutionEvents.groovy.sample",
                "snippet-buildlifecycle-task-execution-events_kotlin_sanityCheck.sample",
                "snippet-buildlifecycle-task-execution-events_kotlin_taskExecutionEvents.kotlin.sample",
                "snippet-custom-model-internal-views_groovy_softwareModelExtend-iv-model.sample",
                "snippet-custom-model-language-type_groovy_softwareModelExtend-components.sample",

                // These snippets are not used in the documentation, but only in the integration tests.
                "snippet-dependency-management-working-with-dependencies-access-metadata-artifact_groovy_accessingMetadataArtifact.sample",
                "snippet-dependency-management-working-with-dependencies-access-metadata-artifact_kotlin_accessingMetadataArtifact.sample",
                "snippet-dependency-management-working-with-dependencies-iterate-artifacts_kotlin_iterating-artifacts.sample",
                "snippet-dependency-management-working-with-dependencies-walk-graph_groovy_walking-dependency-graph.sample",
                "snippet-dependency-management-working-with-dependencies-walk-graph_kotlin_walking-dependency-graph.sample",

                "snippet-ide-eclipse_groovy_wtpWithXml.sample",
                "snippet-ide-eclipse_kotlin_wtpWithXml.sample",
                "snippet-ide-idea-additional-test-sources_groovy_ideaAdditionalTestSources.sample",
                "snippet-ide-idea-additional-test-sources_kotlin_ideaAdditionalTestSources.sample",
                "snippet-ide-idea_groovy_projectWithXml.sample",
                "snippet-ide-idea_kotlin_projectWithXml.sample",
                "snippet-init-scripts-custom-logger_groovy_customLogger.groovy.sample",
                "snippet-init-scripts-custom-logger_kotlin_customLogger.kotlin.sample",
                "snippet-model-rules-basic-rule-source-plugin_groovy_basicRuleSourcePlugin-all.sample",
                "snippet-model-rules-basic-rule-source-plugin_groovy_basicRuleSourcePlugin-model-task.sample",
                "snippet-model-rules-configure-as-required_groovy_modelDslConfigureRuleRunWhenRequired.sample",
                "snippet-model-rules-configure-elements-of-map_groovy_modelDslModelMapNestedAll.sample",
                "snippet-model-rules-initialization-rule-runs-before-configuration-rules_groovy_modelDslInitializationRuleRunsBeforeConfigurationRule.sample",
                "snippet-native-binaries-cpp_groovy_nativeComponentReport.sample",
                "snippet-native-binaries-cunit_groovy_assembleDependentComponents.sample",
                "snippet-native-binaries-cunit_groovy_assembleDependentComponentsReport.sample",
                "snippet-native-binaries-cunit_groovy_buildDependentComponents.sample",
                "snippet-native-binaries-cunit_groovy_buildDependentComponentsReport.sample",
                "snippet-native-binaries-cunit_groovy_completeCUnitExample.sample",
                "snippet-native-binaries-cunit_groovy_dependentComponentsReport.sample",
                "snippet-native-binaries-cunit_groovy_dependentComponentsReportAll.sample",
            ]

            // These tests cover features that the configuration cache doesn't support yet, but we plan to do that before hitting stable.
            // The tests should be removed from this list when the feature becomes supported.
            def testsForNotYetSupportedFeatures = [
                // TODO(https://github.com/gradle/gradle/issues/14880)
                "snippet-dependency-management-working-with-dependencies-iterate-dependencies_groovy_iterating-dependencies.sample",
                "snippet-dependency-management-working-with-dependencies-iterate-dependencies_kotlin_iterating-dependencies.sample",

                // TODO(https://github.com/gradle/gradle/issues/22879) The snippet extracts build logic into a method and calls the method at execution time
                "snippet-tutorial-ant-loadfile-with-method_groovy_antLoadfileWithMethod.sample",
                "snippet-tutorial-ant-loadfile-with-method_kotlin_antLoadfileWithMethod.sample",
            ]

            // Tests that can and has to be fixed to run with the configuration cache enabled.
            // Set the Gradle property runBrokenConfigurationCacheDocsTests=true to run tests from this list or any of the lists above.
            def testsToBeFixedForConfigurationCache = [
                "snippet-build-cache-configure-task_groovy_configureTask.sample",
                "snippet-build-cache-configure-task_kotlin_configureTask.sample",
                // TODO(mlopatkin) These snippets use bintray plugin which is not fully CC-compatible. Remove bintray plugin from samples.
                "snippet-plugins-buildscript_groovy_sanityCheck.sample",
                "snippet-plugins-buildscript_kotlin_sanityCheck.sample",
                "snippet-plugins-dsl_groovy_sanityCheck.sample",
                "snippet-plugins-dsl_kotlin_sanityCheck.sample",
            ]

            def brokenTests = testsForUnsupportedFeatures + testsWithThirdPartyFailures + testsForNotYetSupportedFeatures + testsToBeFixedForConfigurationCache
            brokenTests.forEach { testName ->
                def testMask = "org.gradle.docs.samples.*.$testName"
                if (shouldRunBrokenForConfigurationCacheDocsTests(project)) {
                    includeTestsMatching testMask
                } else {
                    excludeTestsMatching testMask
                }
            }
        }
    } else {
        filter {
            excludeTestsMatching "*WithCC*.sample"
        }
    }
}

// Publications for the docs subproject:

configurations {
    gradleFullDocsElements {
        // TODO: This breaks the provider
        outgoing.artifact(project.gradleDocumentation.getDocumentationRenderedRoot().get().asFile) {
            builtBy 'docs'
        }
    }
}

tasks.named("check") {
    dependsOn(tasks.named('checkstyleApi'))
}

// TODO there is some duplication with DistributionTest.kt here - https://github.com/gradle/gradle-private/issues/3126
class GradleInstallationForTestEnvironmentProvider implements CommandLineArgumentProvider {
    @Internal
    final ConfigurableFileCollection gradleHomeDir

    @PathSensitive(PathSensitivity.RELATIVE)
    @InputDirectory
    final DirectoryProperty samplesdir

    @Nested
    final GradleDistribution gradleDistribution

    private final FileCollection testTaskClasspath
    private final Directory repoRoot

    @Inject
    GradleInstallationForTestEnvironmentProvider(Project project, Test testTask) {
        this.gradleHomeDir = project.objects.fileCollection()
        this.samplesdir = project.objects.directoryProperty()
        this.gradleDistribution = new GradleDistribution(gradleHomeDir)
        this.testTaskClasspath = testTask.classpath
        this.repoRoot = repoRoot(project)
    }

    @Override
    Iterable<String> asArguments() {
        def distributionName = testTaskClasspath.filter { it.name.startsWith("gradle-runtime-api-info") }.singleFile.parentFile.parentFile.parentFile.name
        ["-DintegTest.gradleHomeDir=${gradleHomeDir.singleFile}",
         "-DintegTest.samplesdir=${samplesdir.get().asFile}",
         "-DintegTest.gradleUserHomeDir=${repoRoot.dir("intTestHomeDir/$distributionName")}"]
    }
}

tasks.withType(CheckLinks).configureEach {
    enabled = !gradle.startParameter.taskNames.contains("docs:docsTest")
}

tasks.register("checkLinks") {
    dependsOn(tasks.withType(CheckLinks))
}
