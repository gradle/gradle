// tag::external-task-build[]
plugins {
    id 'groovy'
// end::external-task-build[]
    id 'maven-publish'
// tag::external-task-build[]
}

// tag::gradle-api-dependencies[]
dependencies {
    implementation gradleApi()
}
// end::gradle-api-dependencies[]
// end::external-task-build[]

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'junit:junit:4.13'
}

group = 'org.gradle'
version = '1.0-SNAPSHOT'

publishing {
    repositories {
        maven {
            url = uri(layout.buildDirectory.dir("repo"))
        }
    }
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

// Needed when using ProjectBuilder
class AddOpensArgProvider implements CommandLineArgumentProvider {
    private final Test test;
    public AddOpensArgProvider(Test test) {
        this.test = test;
    }
    @Override
    Iterable<String> asArguments() {
        return test.javaVersion.get().isCompatibleWith(JavaVersion.VERSION_1_9)
            ? ["--add-opens=java.base/java.lang=ALL-UNNAMED"]
            : []
    }
}
tasks.withType(Test).configureEach {
    jvmArgumentProviders.add(new AddOpensArgProvider(it))
}
