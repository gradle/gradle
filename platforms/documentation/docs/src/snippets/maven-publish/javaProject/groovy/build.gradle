plugins {
    id 'java'
    id 'maven-publish'
}

group = 'org.gradle.sample'
version = '1.0'

dependencies {
    implementation 'commons-collections:commons-collections:3.2.2'
}

repositories {
    mavenCentral()
}

// tag::publish-modify-component[]
java {
    withJavadocJar()
    withSourcesJar()
}

components.java.withVariantsFromConfiguration(configurations.sourcesElements) {
    skip()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}
// end::publish-modify-component[]

// tag::repo-url-from-variable[]
// tag::repo-url-from-project-property[]
publishing {
    repositories {
        maven {
            def releasesRepoUrl = layout.buildDirectory.dir('repos/releases')
            def snapshotsRepoUrl = layout.buildDirectory.dir('repos/snapshots')
// end::repo-url-from-variable[]
            url = project.hasProperty('release') ? releasesRepoUrl : snapshotsRepoUrl
// end::repo-url-from-project-property[]
// tag::repo-url-from-variable[]
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
// tag::repo-url-from-project-property[]
        }
    }
}
// end::repo-url-from-project-property[]
// end::repo-url-from-variable[]
