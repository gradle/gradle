plugins {
    id 'java'
}

repositories {
    mavenCentral()
}

dependencies {
    // We should use a legacy version to support running on jdk6
    implementation 'commons-lang:commons-lang:2.6'
    testImplementation 'junit:junit:4.+'
}

// tag::java-cross-compilation[]
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(7)
    }
}
// end::java-cross-compilation[]

tasks.withType(Test) {
    systemProperty('targetJavaVersion', project.findProperty('targetJavaVersion'))
    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(8) // Only compilation is supported < 8. Tests are not.
    }
}

tasks.register('checkJavadocOutput') {
    dependsOn javadoc
    def docsDir = java.docsDir
    doLast {
        assert new File(docsDir.get().asFile, 'javadoc/org/gradle/Person.html').text.contains('<p>Represents a person.</p>')
    }
}

build.dependsOn checkJavadocOutput

