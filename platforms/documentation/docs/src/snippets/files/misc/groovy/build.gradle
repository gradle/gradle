import java.nio.file.Files

// tag::mkdir-example[]
tasks.register('ensureDirectory') {
    // Store target directory into a variable to avoid project reference in the configuration cache
    def directory = file("images")

    doLast {
        Files.createDirectories(directory.toPath())
    }
}
// end::mkdir-example[]

// tag::move-example[]
tasks.register('moveReports') {
    // Store the build directory into a variable to avoid project reference in the configuration cache
    def dir = buildDir

    doLast {
        ant.move file: "${dir}/reports",
                 todir: "${dir}/toArchive"
    }
}
// end::move-example[]

// tag::delete-example[]
tasks.register('myClean', Delete) {
    delete buildDir
}
// end::delete-example[]

// tag::delete-with-filter-example[]
tasks.register('cleanTempFiles', Delete) {
    delete fileTree("src").matching {
        include "**/*.tmp"
    }
}
// end::delete-with-filter-example[]
