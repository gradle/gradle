plugins {
    id 'java'
}

// tag::file-deps[]
repositories {
    flatDir {
        name = 'libs dir'
        dir file('libs')  // <1>
    }
}

dependencies {
    implementation files('libs/our-custom.jar')  // <2>
    implementation ':awesome-framework:2.0'  // <3>
    implementation ':utility-library:1.0'  // <3>
}
// end::file-deps[]

// tag::retrieve-deps[]
tasks.register('retrieveRuntimeDependencies', Copy) {
    into layout.buildDirectory.dir('libs')
    from configurations.runtimeClasspath
}
// end::retrieve-deps[]

// tag::properties[]
def tmpDistDir = layout.buildDirectory.dir('dist')

tasks.register('javadocJarArchive', Jar) {
    from javadoc  // <1>
    archiveClassifier = 'javadoc'
}

tasks.register('unpackJavadocs', Copy) {
    from zipTree(javadocJarArchive.archiveFile)  // <2>
    into tmpDistDir  // <3>
}
// end::properties[]
