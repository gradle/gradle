// tag::do-this[]
abstract class GreetingTask extends DefaultTask {
    @Input
    abstract Property<String> getType()

    @OutputDirectory
    abstract DirectoryProperty getOutputDirectory()

    @TaskAction
    void run() {
        def outFileName = type.get() + ".txt"
        def outFile = outputDirectory.file(outFileName).get().asFile
        outFile.text = "Hello" // <1>
    }
}

abstract class ConsumerTask extends DefaultTask {
    @InputFile
    abstract RegularFileProperty getInputFile()
    @OutputFile
    abstract RegularFileProperty getOutputFile()

    @TaskAction
    void run() {
        def outputText = inputFile.get().asFile.text
        outputFile.get().asFile.write(outputText) // <2>
    }
}

def taskA = tasks.register("taskA", GreetingTask) {
    type = "a"
    outputDirectory = layout.buildDirectory.dir("shared") // <3>
}
tasks.register("taskB", GreetingTask) {
    type = "b"
    outputDirectory = layout.buildDirectory.dir("shared") // <4>
}
tasks.register("consumer", ConsumerTask) {
    inputFile = taskA.flatMap { it.outputDirectory.file("a.txt") }  // <5>
    outputFile = layout.buildDirectory.file("consumerOutput.txt")
}
// end::do-this[]
