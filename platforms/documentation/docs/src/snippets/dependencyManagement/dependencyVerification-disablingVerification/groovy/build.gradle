repositories {
    mavenCentral()
}

configurations {
    myPlugin {
        canBeConsumed = false
        canBeResolved = false
    }
    myPluginClasspath {
        extendsFrom(myPlugin)
        canBeConsumed = false
    }
}

dependencies {
    myPlugin "org.apache.commons:commons-lang3:3.3.1"
}

// tag::disabling-one-configuration[]
configurations {
    myPluginClasspath {
        resolutionStrategy {
            disableDependencyVerification()
        }
    }
}
// end::disabling-one-configuration[]

tasks.register("checkDependencies") {
    FileCollection classpath = configurations.myPluginClasspath
    inputs.files(classpath)
    doLast {
        println(classpath.files)
    }
}

// tag::disabling-detached-configuration[]
tasks.register("checkDetachedDependencies") {
    def detachedConf = configurations.detachedConfiguration(dependencies.create("org.apache.commons:commons-lang3:3.3.1"))
    detachedConf.resolutionStrategy.disableDependencyVerification()
    doLast {
        println(detachedConf.files)
    }
}
// end::disabling-detached-configuration[]
