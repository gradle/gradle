plugins {
    id("java-library")
}

repositories {
    mavenCentral()
}

// tag::component-filter-dependencies[]
dependencies {
    implementation(project(":other"))
    implementation("org.junit.jupiter:junit-jupiter-api:5.11.0")
}
// end::component-filter-dependencies[]

abstract class ResolveFiles extends DefaultTask {

    @InputFiles
    abstract ConfigurableFileCollection getFiles()

    @TaskAction
    void print() {
        files.each {
            println(it.name)
        }
    }
}

// tag::resolve-component-filter[]
tasks.register("resolveProjects", ResolveFiles) {
    files.from(configurations.runtimeClasspath.incoming.artifactView {
        componentFilter {
            it instanceof ProjectComponentIdentifier
        }
    }.files)
}
tasks.register("resolveModules", ResolveFiles) {
// end::resolve-component-filter[]
    dependsOn(tasks.named("resolveProjects")) // To preserve output ordering
// tag::resolve-component-filter[]
    files.from(configurations.runtimeClasspath.incoming.artifactView {
        componentFilter {
            it instanceof ModuleComponentIdentifier
        }
    }.files)
}
// end::resolve-component-filter[]
