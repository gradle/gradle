// tag::repository-filter[]
repositories {
    maven {
        url = "https://repo.mycompany.com/maven2"
        content {
            // this repository *only* contains artifacts with group "my.company"
            includeGroup "my.company"
        }
    }
    mavenCentral {
        content {
            // this repository contains everything BUT artifacts with group starting with "my.company"
            excludeGroupByRegex "my\\.company.*"
        }
    }
}
// end::repository-filter[]

// tag::exclusive-repository-filter[]
repositories {
    // This repository will _not_ be searched for artifacts in my.company
    // despite being declared first
    mavenCentral()
    exclusiveContent {
        forRepository {
            maven {
                url = "https://repo.mycompany.com/maven2"
            }
        }
        filter {
            // this repository *only* contains artifacts with group "my.company"
            includeGroup "my.company"
        }
    }
}
// end::exclusive-repository-filter[]

// tag::repository-snapshots[]
repositories {
    maven {
        url = "https://repo.mycompany.com/releases"
        mavenContent {
            releasesOnly()
        }
    }
    maven {
        url = "https://repo.mycompany.com/snapshots"
        mavenContent {
            snapshotsOnly()
        }
    }
}
// end::repository-snapshots[]

configurations {
    libs
}

dependencies {
    libs "com.google.guava:guava:23.0"
}

tasks.register('copyLibs', Copy) {
    from configurations.libs
    into layout.buildDirectory.dir('libs')
}
