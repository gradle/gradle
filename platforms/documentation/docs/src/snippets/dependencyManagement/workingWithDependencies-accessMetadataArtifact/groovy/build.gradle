// tag::accessing-metadata-artifact[]
plugins {
    id 'java-library'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.guava:guava:18.0'
}

tasks.register('printGuavaMetadata') {
    dependsOn configurations.compileClasspath

    doLast {
        ArtifactResolutionQuery query = dependencies.createArtifactResolutionQuery()
            .forModule('com.google.guava', 'guava', '18.0')
            .withArtifacts(MavenModule, MavenPomArtifact)
        ArtifactResolutionResult result = query.execute()

        for(component in result.resolvedComponents) {
            Set<ArtifactResult> mavenPomArtifacts = component.getArtifacts(MavenPomArtifact)
            ArtifactResult guavaPomArtifact = mavenPomArtifacts.find { it.file.name == 'guava-18.0.pom' }
            def xml = new groovy.xml.XmlSlurper().parse(guavaPomArtifact.file)
            println guavaPomArtifact.file.name
            println xml.name
            println xml.description
        }
    }
}
// end::accessing-metadata-artifact[]
