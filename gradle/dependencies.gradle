/*
 * Copyright 2012 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

ext {
    versions = [:]
    libraries = [:]
}

versions.commons_io = 'commons-io:commons-io:2.2'

versions.groovy = "2.4.11"

versions.bouncycastle = "1.51"

versions.maven = "3.0.4"

libraries.ant = dependencies.module('org.apache.ant:ant:1.9.6') {
    dependency 'org.apache.ant:ant-launcher:1.9.6@jar'
}

libraries.asm =  'org.ow2.asm:asm-debug-all:6.0_ALPHA'
libraries.cglib = 'cglib:cglib:3.2.5'
libraries.bndlib = dependencies.module('biz.aQute.bnd:biz.aQute.bndlib:3.4.0')
libraries.commons_cli = 'commons-cli:commons-cli:1.2@jar'
libraries.commons_io = dependencies.module(versions.commons_io)
libraries.commons_lang = 'commons-lang:commons-lang:2.6@jar'
libraries.commons_collections = 'commons-collections:commons-collections:3.2.2@jar'
libraries.commons_compress = 'org.apache.commons:commons-compress:1.14'
libraries.jsch = "com.jcraft:jsch:0.1.54"
libraries.ivy = dependencies.module('org.apache.ivy:ivy:2.2.0'){
    dependency libraries.jsch
}
libraries.jcip = "net.jcip:jcip-annotations:1.0@jar"
libraries.inject = dependencies.module('javax.inject:javax.inject:1')

// Logging
libraries.slf4j_api = dependencies.module('org.slf4j:slf4j-api:1.7.10')
libraries.jcl_to_slf4j = dependencies.module('org.slf4j:jcl-over-slf4j:1.7.10') {
    dependency libraries.slf4j_api
}
libraries.jul_to_slf4j = dependencies.module('org.slf4j:jul-to-slf4j:1.7.10') {
    dependency libraries.slf4j_api
}
libraries.log4j_to_slf4j = dependencies.module('org.slf4j:log4j-over-slf4j:1.7.10') {
    dependency libraries.slf4j_api
}
libraries.logback_core = dependencies.module('ch.qos.logback:logback-core:1.0.13')
libraries.logback_classic = dependencies.module('ch.qos.logback:logback-classic:1.0.13') {
    dependency libraries.logback_core
    dependency libraries.slf4j_api
}

// Jetty
libraries.servlet_api = "org.mortbay.jetty:servlet-api:2.5-20081211@jar"
libraries.jetty_util = dependencies.module("org.mortbay.jetty:jetty-util:6.1.26") {
    dependency libraries.slf4j_api
    dependency libraries.servlet_api
}
libraries.jetty = dependencies.module("org.mortbay.jetty:jetty:6.1.26") {
    dependency libraries.jetty_util
    dependency libraries.servlet_api
}

libraries.commons_codec = "commons-codec:commons-codec:1.6@jar"
libraries.jcifs = "org.samba.jcifs:jcifs:1.3.17@jar"
libraries.commons_httpclient = dependencies.module('org.apache.httpcomponents:httpclient:4.4.1') {
    dependency "org.apache.httpcomponents:httpcore:4.4.4@jar"
    dependency libraries.jcl_to_slf4j
    dependency libraries.commons_codec
    dependency libraries.jcifs
}


libraries += [
    jatl: 'com.googlecode.jatl:jatl:0.2.2',
    dom4j: 'dom4j:dom4j:1.6.1@jar',
    guava: 'com.google.guava:guava-jdk5:17.0@jar',
    kryo: 'com.esotericsoftware.kryo:kryo:2.20',
    jsr305: 'com.google.code.findbugs:jsr305:1.3.9@jar',
    groovy: "org.codehaus.groovy:groovy-all:${versions.groovy}",
    jaxen: 'jaxen:jaxen:1.1@jar',
    jcip: "net.jcip:jcip-annotations:1.0@jar",
    junit: 'junit:junit:4.12@jar',
    xmlunit: 'xmlunit:xmlunit:1.3',
    equalsVerifier: 'nl.jqno.equalsverifier:equalsverifier:2.1.6',
    nekohtml: 'net.sourceforge.nekohtml:nekohtml:1.9.14',
    xbean: 'org.apache.xbean:xbean-reflect:3.4@jar', //required by maven3 classes
    nativePlatform: 'net.rubygrapefruit:native-platform:0.14',
    jansi: dependencies.module('org.fusesource.jansi:jansi:1.14'),
    xerces: "xerces:xercesImpl:2.9.1",
    objenesis: 'org.objenesis:objenesis:1.2@jar',
    jsoup:'org.jsoup:jsoup:1.6.3'
]

libraries.maven3 = dependencies.module("org.apache.maven:maven-core:${versions.maven}") {
    dependency "org.apache.maven:maven-settings:${versions.maven}@jar"
    dependency "org.apache.maven:maven-settings-builder:${versions.maven}@jar"

    //plexus:
    dependency "org.codehaus.plexus:plexus-utils:2.0.6@jar"
    dependency "org.codehaus.plexus:plexus-interpolation:1.14@jar"
    dependency "org.codehaus.plexus:plexus-component-annotations:1.5.5@jar"
    dependency "org.codehaus.plexus:plexus-container-default:1.5.5@jar"
    dependency "org.codehaus.plexus:plexus-classworlds:2.4@jar"

    //sonatype plexus
    dependency "org.sonatype.plexus:plexus-cipher:1.7@jar"
    dependency "org.sonatype.plexus:plexus-sec-dispatcher:1.3@jar"

    //core:
    dependency "org.apache.maven:maven-core:${versions.maven}@jar"
    dependency "org.apache.maven:maven-compat:${versions.maven}@jar"
    dependency "org.apache.maven:maven-model-builder:${versions.maven}@jar"
    dependency "org.apache.maven:maven-model:${versions.maven}@jar"

    //somewhat core:
    dependency "org.apache.maven:maven-artifact:${versions.maven}@jar"
    dependency "org.apache.maven:maven-repository-metadata:${versions.maven}@jar"
    dependency "org.apache.maven:maven-plugin-api:${versions.maven}@jar"
    dependency "org.apache.maven:maven-aether-provider:${versions.maven}@jar"

    dependency 'org.apache.maven.wagon:wagon-file:2.4@jar'
    dependency 'org.apache.maven.wagon:wagon-http:2.4@jar'
    dependency 'org.apache.maven.wagon:wagon-provider-api:2.4@jar'
    dependency 'org.apache.maven.wagon:wagon-http-shared4:2.4@jar'

    //eather:
    dependency "org.sonatype.aether:aether-api:1.13.1@jar"
    dependency "org.sonatype.aether:aether-impl:1.13.1@jar"
    dependency "org.sonatype.aether:aether-spi:1.13.1@jar"
    dependency "org.sonatype.aether:aether-util:1.13.1@jar"
    dependency 'org.sonatype.aether:aether-connector-wagon:1.13.1@jar'
}

libraries.spock = [
    'org.spockframework:spock-core:1.0-groovy-2.4@jar',
    libraries.groovy,
    libraries.objenesis,
    libraries.cglib
]
libraries.jmock = [
    'org.jmock:jmock:2.5.1',
    'org.hamcrest:hamcrest-core:1.3',
    'org.hamcrest:hamcrest-library:1.3',
    dependencies.create('org.jmock:jmock-junit4:2.5.1') { exclude group: 'junit', module: 'junit-dep' }, //junit-dep pulls old definitions of core junit types.
    'org.jmock:jmock-legacy:2.5.1',
    libraries.objenesis,
    libraries.cglib
]

versions.findbugs = "2.0.1"
libraries.findbugs = dependencies.module("com.google.code.findbugs:findbugs:${versions.findbugs}") {
    dependency libraries.dom4j
}
libraries.bcel = "com.google.code.findbugs:bcel:${versions.findbugs}@jar"

libraries.gson = "com.google.code.gson:gson:2.7"
libraries.mina = dependencies.module("org.apache.mina:mina-core:2.0.10") {
    dependency libraries.slf4j_api
}
libraries.sshd = dependencies.module("org.apache.sshd:sshd-core:1.2.0") {
    dependency libraries.slf4j_api
    dependency libraries.mina
}

libraries.bouncycastle_provider = "org.bouncycastle:bcprov-jdk15on:${versions.bouncycastle}@jar"
libraries.bouncycastle_pgp = dependencies.module("org.bouncycastle:bcpg-jdk15on:${versions.bouncycastle}") {
    dependency libraries.bouncycastle_provider
}

libraries.joda = 'joda-time:joda-time:2.8.2@jar'

versions.aws = "1.11.6"
versions.jackson = "2.6.6"
libraries.awsS3 = [
        "com.amazonaws:aws-java-sdk-s3:${versions.aws}@jar",
        "com.amazonaws:aws-java-sdk-kms:${versions.aws}@jar",
        "com.amazonaws:aws-java-sdk-core:${versions.aws}@jar",
        "com.fasterxml.jackson.core:jackson-core:${versions.jackson}@jar",
        "com.fasterxml.jackson.core:jackson-annotations:${versions.jackson}@jar",
        "com.fasterxml.jackson.core:jackson-databind:${versions.jackson}@jar"
] + libraries.commons_httpclient + libraries.joda

// keep in sync with ScalaLanguagePlugin code
libraries.zinc = 'com.typesafe.zinc:zinc:0.3.15'

libraries.gcs = [
        dependencies.create('com.google.apis:google-api-services-storage:v1-rev78-1.22.0') {
            exclude group: 'commons-logging', module: 'commons-logging'
        },
        "com.fasterxml.jackson.core:jackson-core:${versions.jackson}@jar",
        "com.fasterxml.jackson.core:jackson-annotations:${versions.jackson}@jar",
        "com.fasterxml.jackson.core:jackson-databind:${versions.jackson}@jar"
] + libraries.commons_httpclient + libraries.joda

// for XCode IDE integration support
libraries.plist = 'com.googlecode.plist:dd-plist:1.20'

allprojects {
    configurations.all {
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'org.ow2.asm') {
                details.useTarget(libraries.asm)
            } else if (details.requested.group == 'org.codehaus.groovy') {
                details.useTarget(libraries.groovy)
            } else if (details.requested.group == 'cglib') {
                details.useTarget(libraries.cglib)
            }
        }
    }
}
