/*
 * Copyright 2012 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

ext {
    libraries = [:]
}

libraries.aether_connector =    'org.sonatype.aether:aether-connector-wagon'
libraries.ant =                 'org.apache.ant:ant'
libraries.asm =                 'org.ow2.asm:asm'
libraries.asm_commons =         'org.ow2.asm:asm-commons'
libraries.asm_tree =            'org.ow2.asm:asm-tree'
libraries.asm_util =            'org.ow2.asm:asm-util'
libraries.awsS3_core =          'com.amazonaws:aws-java-sdk-core'
libraries.awsS3_s3 =            'com.amazonaws:aws-java-sdk-s3'
libraries.awsS3_kms =           'com.amazonaws:aws-java-sdk-kms'
libraries.bouncycastle_provider = 'org.bouncycastle:bcprov-jdk15on'
libraries.bouncycastle_pgp =    'org.bouncycastle:bcpg-jdk15on'
libraries.bndlib =              'biz.aQute.bnd:biz.aQute.bndlib'
libraries.bsh =                 'org.apache-extras.beanshell:bsh'
libraries.commons_cli =         'commons-cli:commons-cli'
libraries.commons_codec =       'commons-codec:commons-codec'
libraries.commons_collections = 'commons-collections:commons-collections'
libraries.commons_compress =    'org.apache.commons:commons-compress'
libraries.commons_httpclient =  'org.apache.httpcomponents:httpclient'
libraries.commons_io =          'commons-io:commons-io'
libraries.commons_lang =        'commons-lang:commons-lang'
libraries.fastutil =            'it.unimi.dsi:fastutil'
libraries.gcs =                 'com.google.apis:google-api-services-storage'
libraries.groovy =              'org.codehaus.groovy:groovy-all'
libraries.gson =                'com.google.code.gson:gson'
libraries.guava =               'com.google.guava:guava-jdk5'
libraries.inject =              'javax.inject:javax.inject'
libraries.ivy =                 'org.apache.ivy:ivy'
libraries.jackson_core =        'com.fasterxml.jackson.core:jackson-core'
libraries.jackson_annotations = 'com.fasterxml.jackson.core:jackson-annotations'
libraries.jackson_databind =    'com.fasterxml.jackson.core:jackson-databind'
libraries.jansi =               'org.fusesource.jansi:jansi'
libraries.jatl =                'com.googlecode.jatl:jatl'
libraries.jcifs =               'org.samba.jcifs:jcifs'
libraries.jcip =                'net.jcip:jcip-annotations'
libraries.jgit =                'org.eclipse.jgit:org.eclipse.jgit'
libraries.joda =                'joda-time:joda-time'
libraries.jsch =                'com.jcraft:jsch'
libraries.jsr305 =              'com.google.code.findbugs:jsr305'
libraries.junit =               'junit:junit'
libraries.kryo =                'com.esotericsoftware.kryo:kryo'
libraries.maven3 =              'org.apache.maven:maven-core'
libraries.maven3_wagon_file =   'org.apache.maven.wagon:wagon-file'
libraries.maven3_wagon_http =   'org.apache.maven.wagon:wagon-http'
libraries.nativePlatform =      'net.rubygrapefruit:native-platform'
libraries.nekohtml =            'net.sourceforge.nekohtml:nekohtml'
libraries.objenesis =           'org.objenesis:objenesis'
libraries.plexus_container =    'org.codehaus.plexus:plexus-container-default'
libraries.plist =               'com.googlecode.plist:dd-plist' // for XCode IDE integration support
libraries.pmaven_common =       'org.sonatype.pmaven:pmaven-common'
libraries.pmaven_groovy =       'org.sonatype.pmaven:pmaven-groovy'
libraries.rhino =               'org.mozilla:rhino'
libraries.simple =              'org.simpleframework:simple'
libraries.testng =              'org.testng:testng'
libraries.xerces =              'xerces:xercesImpl'
libraries.xmlApis =             'xml-apis:xml-apis'
libraries.slf4j_api =           'org.slf4j:slf4j-api'
libraries.jcl_to_slf4j =        'org.slf4j:jcl-over-slf4j'
libraries.jul_to_slf4j =        'org.slf4j:jul-to-slf4j'
libraries.log4j_to_slf4j =      'org.slf4j:log4j-over-slf4j'

// the 'if' condition here is a configuration time performance optimization, that applied the constraints only to root projects of the subproject dependencies tree
subprojects { project -> if (project.name in ['baseServices', 'cli']) {
    configurations.whenObjectAdded {
        if (name == 'compile') {
            dependencies {
                constraints {
                    // lock dependencies that are part of the Gradle distribution
                    compile(libraries.aether_connector) { version { strictly versions.aether } }
                    compile(libraries.ant) { version { strictly versions.ant } }
                    compile(libraries.asm) { version { strictly versions.asm } }
                    compile(libraries.asm_commons) { version { strictly versions.asm } }
                    compile(libraries.asm_tree) { version { strictly versions.asm } }
                    compile(libraries.awsS3_core) { version { strictly versions.aws } }
                    compile(libraries.awsS3_kms) { version { strictly versions.aws } }
                    compile(libraries.awsS3_s3) { version { strictly versions.aws } }
                    compile(libraries.bouncycastle_provider) { version { strictly versions.bouncycastle } }
                    compile(libraries.bouncycastle_pgp) { version { strictly versions.bouncycastle } }
                    compile(libraries.bndlib) { version { strictly '3.4.0' } }
                    compile(libraries.bsh) { version { strictly '2.0b6' } }
                    compile(libraries.commons_cli) { version { strictly '1.2' } }
                    compile(libraries.commons_codec) { version { strictly '1.9' } }
                    compile(libraries.commons_collections) { version { strictly '3.2.2' } }
                    compile(libraries.commons_compress) { version { strictly versions.commons_compress } }
                    compile(libraries.commons_httpclient) { version { strictly '4.5.2' } }
                    compile(libraries.commons_io) { version { strictly '2.4' } }
                    compile(libraries.commons_lang) { version { strictly '2.6' } }
                    compile(libraries.fastutil) { version { strictly '7.2.1' } }
                    compile(libraries.gcs) { version { strictly 'v1-rev116-1.23.0' } }
                    compile(libraries.groovy) { version { strictly versions.groovy } }
                    compile(libraries.gson) { version { strictly '2.7' } }
                    compile(libraries.guava) { version { strictly '17.0' } }
                    compile(libraries.inject) { version { strictly '1' } }
                    compile(libraries.ivy) { version { strictly '2.2.0' } }
                    compile(libraries.jackson_annotations) { version { strictly versions.jackson } }
                    compile(libraries.jackson_core) { version { strictly versions.jackson } }
                    compile(libraries.jackson_databind) { version { strictly versions.jackson } }
                    compile(libraries.jansi) { version { strictly '1.14' } }
                    compile(libraries.jatl) { version { strictly '0.2.2' } }
                    compile(libraries.jcifs) { version { strictly '1.3.17' } }
                    compile(libraries.jcip) { version { strictly '1.0' } }
                    compile(libraries.jgit) { version { strictly '4.5.3.201708160445-r' }; because '4.6+ requires Java 8' }
                    compile(libraries.joda) { version { strictly '2.8.2' } }
                    compile(libraries.jsch) { version { strictly '0.1.54' } }
                    compile(libraries.jsr305) { version { strictly '2.0.1' } }
                    compile(libraries.junit) { version { strictly '4.12' } }
                    compile(libraries.kryo) { version { strictly '2.20' } }
                    compile(libraries.maven3_wagon_file) { version { strictly versions.maven_wagon } }
                    compile(libraries.maven3_wagon_http) { version { strictly versions.maven_wagon } }
                    compile(libraries.maven3) { version { strictly versions.maven } }
                    compile(libraries.nativePlatform) { version { strictly '0.14' } }
                    compile(libraries.nekohtml) { version { strictly '1.9.20' } }
                    compile(libraries.objenesis) { version { strictly '1.2' } }
                    compile(libraries.plexus_container) { version { strictly '1.5.5' } }
                    compile(libraries.plist) { version { strictly '1.20' } }
                    compile(libraries.pmaven_common) { version { strictly versions.maven_polyglot } }
                    compile(libraries.pmaven_groovy) { version { strictly versions.maven_polyglot } }
                    compile(libraries.rhino) { version { strictly '1.7R3' } }
                    compile(libraries.simple) { version { strictly '4.1.21' } }
                    compile(libraries.testng) { version { strictly '6.3.1' } }
                    compile(libraries.xerces) { version { strictly '2.11.0' } }
                    compile(libraries.xmlApis) { version { strictly '1.4.01' }; because 'Do not upgrade to 2.0.x, which has a POM with relocation Gradle does not handle well' }
                    compile(libraries.slf4j_api) { version { strictly versions.slf4j } }
                    compile(libraries.jcl_to_slf4j) { version { strictly versions.slf4j } }
                    compile(libraries.jul_to_slf4j) { version { strictly versions.slf4j } }
                    compile(libraries.log4j_to_slf4j) { version { strictly versions.slf4j } }

                    // lock transitive dependencies that are part of the Gradle distribution
                    compile('org.jetbrains:annotations') { version { strictly '13.0' } }
                    compile('org.apache.ant:ant-launcher') { version { strictly versions.ant } }
                    compile('org.ow2.asm:asm-util') { version { strictly versions.asm } }
                    compile('com.esotericsoftware.minlog:minlog') { version { strictly '1.2' } }
                    compile('org.sonatype.aether:aether-api') { version { strictly versions.aether } }
                    compile('org.sonatype.aether:aether-impl') { version { strictly versions.aether } }
                    compile('org.sonatype.aether:aether-spi') { version { strictly versions.aether } }
                    compile('org.sonatype.aether:aether-util') { version { strictly versions.aether } }
                    compile('com.google.api-client:google-api-client') { version { strictly '1.23.0' } }
                    compile('com.google.http-client:google-http-client') { version { strictly '1.23.0' } }
                    compile('com.google.http-client:google-http-client-jackson2') { version { strictly '1.23.0' } }
                    compile('com.google.oauth-client:google-oauth-client') { version { strictly '1.23.0' } }
                    compile('org.hamcrest:hamcrest-core') { version { strictly '1.3' } }
                    compile('org.apache.httpcomponents:httpcore') { version { strictly '4.4.9' } }
                    compile('com.beust:jcommander') { version { strictly '1.47' } }
                    compile('org.apache.maven:maven-aether-provider') { version { strictly versions.maven } }
                    compile('org.apache.maven:maven-artifact') { version { strictly versions.maven } }
                    compile('org.apache.maven:maven-compat') { version { strictly versions.maven } }
                    compile('org.apache.maven:maven-model') { version { strictly versions.maven } }
                    compile('org.apache.maven:maven-model-builder') { version { strictly versions.maven } }
                    compile('org.apache.maven:maven-plugin-api') { version { strictly versions.maven } }
                    compile('org.apache.maven:maven-repository-metadata') { version { strictly versions.maven } }
                    compile('org.apache.maven:maven-settings') { version { strictly versions.maven } }
                    compile('org.apache.maven:maven-settings-builder') { version { strictly versions.maven } }
                    compile('org.sonatype.plexus:plexus-cipher') { version { strictly '1.7' } }
                    compile('org.codehaus.plexus:plexus-classworlds') { version { strictly '2.4' } }
                    compile('org.codehaus.plexus:plexus-component-annotations') { version { strictly '1.5.5' } }
                    compile('org.codehaus.plexus:plexus-interpolation') { version { strictly '1.14' } }
                    compile('org.codehaus.plexus:plexus-sec-dispatcher') { version { strictly '1.3' } }
                    compile('org.codehaus.plexus:plexus-utils') { version { strictly '3.0.8' } }
                    compile('org.yaml:snakeyaml:1.6') { version { strictly '1.6' } } //added by testng, could be avoided with newer TestNG version
                    compile('org.apache.maven.wagon:wagon-http-shared4') { version { strictly versions.maven_wagon } }
                    compile('org.apache.maven.wagon:wagon-provider-api') { version { strictly versions.maven_wagon } }
                    compile('org.apache.xbean:xbean-reflect') { version { strictly '3.4' } }

                    // Reject dependencies we do not want completely
                    compile('com.google.collections:google-collections') {
                        version { rejectAll() }
                        because 'Guava replaces google collections'
                    }
                    compile('junit:junit-dep') {
                        version { rejectAll() }
                        because 'junit:junit has replaced junit:junit-dep'
                    }
                    compile('commons-logging:commons-logging') {
                        version { rejectAll() }
                        because 'We do not want non-slf4j logging compiles on the classpath'
                    }
                    compile('org.sonatype.sisu:sisu-inject-plexus') {
                        version { rejectAll() }
                        because 'We do not wand this dependency injection on the classpath'
                    }
                }
            }
        }
    }
}}

subprojects {
    dependencies {
        components { handler ->
            // Gradle distribution - minify: remove unused transitive dependencies
            withModule('org.apache.maven:maven-core') {
                allVariants {
                    withDependencies {
                        it.removeAll {
                            it.name != 'maven-settings-builder' &&
                                it.name != 'maven-model' &&
                                it.name != 'maven-model-builder' &&
                                it.name != 'maven-artifact' &&
                                it.name != 'maven-aether-provider' &&
                                it.group != 'org.sonatype.aether'
                        }
                    }
                }
            }
            withModule('com.amazonaws:aws-java-sdk-core') {
                allVariants {
                    withDependencies { it.removeAll { it.name == 'jackson-dataformat-cbor' } }
                }
            }
            withModule('org.eclipse.jgit:org.eclipse.jgit') {
                allVariants {
                    withDependencies { it.removeAll { it.group == 'com.googlecode.javaewah' }}
                }
            }
            withModule('org.apache.maven.wagon:wagon-http-shared4') {
                allVariants {
                    withDependencies { it.removeAll { it.group == 'org.jsoup' }}
                }
            }
            withModule('org.sonatype.aether:aether-connector-wagon') {
                allVariants {
                    withDependencies { it.removeAll { it.group == 'org.sonatype.sisu' } }
                }
            }
            withModule('org.apache.maven:maven-compat') {
                allVariants {
                    withDependencies { it.removeAll { it.group == 'org.sonatype.sisu' } }
                }
            }
            withModule('org.apache.maven:maven-plugin-api') {
                allVariants {
                    withDependencies { it.removeAll { it.group == 'org.sonatype.sisu' } }
                }
            }

            // Gradle distribution - replace similar library with different coordinates
            replaceGoogleCollectionsWithGuava(handler, 'org.codehaus.plexus:plexus-container-default')

            replaceJunitDepWithJunit(handler, 'org.jmock:jmock-junit4')

            replaceBeanshellWithApacheBeanshell(handler, 'org.testng:testng')

            replaceLog4JWithAdapter(handler, 'org.apache.xbean:xbean-reflect')

            replaceJCLWithAdapter(handler, 'org.apache.xbean:xbean-reflect')
            replaceJCLWithAdapter(handler, 'org.apache.httpcomponents:httpclient')
            replaceJCLWithAdapter(handler, 'com.amazonaws:aws-java-sdk-core')
            replaceJCLWithAdapter(handler, 'org.apache.httpcomponents:httpmime')
            replaceJCLWithAdapter(handler, 'net.sourceforge.htmlunit:htmlunit')
            replaceJCLWithAdapter(handler, 'org.apache.maven.wagon:wagon-http')
            replaceJCLWithAdapter(handler, 'org.apache.maven.wagon:wagon-http-shared4')

            replaceJCLConstraintWithAdapter(handler, 'org.codehaus.groovy:groovy-all')

            //TODO check if we can upgrade the following dependencies and remove the rules
            downgradeIvy(handler, 'org.codehaus.groovy:groovy-all')
            downgradeTestNG(handler, 'org.codehaus.groovy:groovy-all')

            downgradeXmlApis(handler, 'jaxen:jaxen')
            downgradeXmlApis(handler, 'jdom:jdom')
            downgradeXmlApis(handler, 'xalan:xalan')
            downgradeXmlApis(handler, 'jaxen:jaxen')
        }
    }
}

def replaceJCLWithAdapter(ComponentMetadataHandler handler, String module) {
    handler.withModule(module) {
        allVariants {
            withDependencies {
                it.removeAll { it.group == 'commons-logging' }
                it.add ('org.slf4j:jcl-over-slf4j:1.7.10') {
                    because 'We do not want non-slf4j logging implementations on the classpath'
                }
            }
        }
    }
}

def replaceJCLConstraintWithAdapter(ComponentMetadataHandler handler, String module) {
    handler.withModule(module) {
        allVariants {
            withDependencyConstraints {
                it.removeAll { it.group == 'commons-logging' }
                it.add ('org.slf4j:jcl-over-slf4j:1.7.10') {
                    because 'We do not want non-slf4j logging implementations on the classpath'
                }
            }
        }
    }
}

def replaceLog4JWithAdapter(ComponentMetadataHandler handler, String module) {
    handler.withModule(module) {
        allVariants {
            withDependencies {
                it.removeAll { it.group == 'log4j' }
                it.add ('org.slf4j:log4j-over-slf4j:1.7.16') {
                    because 'We do not want non-slf4j logging implementations on the classpath'
                }
            }
        }
    }
}

def replaceGoogleCollectionsWithGuava(ComponentMetadataHandler handler, String module) {
    handler.withModule(module) {
        allVariants {
            withDependencies {
                it.removeAll { it.group == 'com.google.collections' }
                it.add('com.google.guava:guava-jdk5:17.0') {
                    because 'Guava replaces google collections'
                }
            }
        }
    }
}

def replaceJunitDepWithJunit(ComponentMetadataHandler handler, String module) {
    handler.withModule(module) {
        allVariants {
            withDependencies {
                it.removeAll { it.group == 'junit' }
                it.add('junit:junit:4.12') {
                    because 'junit:junit replaced junit:junit-dep'
                }
            }
        }
    }
}

def replaceBeanshellWithApacheBeanshell(ComponentMetadataHandler handler, String module) {
    handler.withModule(module) {
        allVariants {
            withDependencies {
                it.removeAll { it.group == 'org.beanshell' }
                it.add('org.apache-extras.beanshell:bsh:2.0b6') {
                    because 'org.apache-extras.beanshell:bsh replaced junit:junit-dep'
                }
            }
        }
    }
}

def downgradeIvy(ComponentMetadataHandler handler, String module) {
    handler.withModule(module) {
        allVariants {
            withDependencyConstraints {
                it.findAll { it.group == 'org.apache.ivy' }.each {
                    it.version { prefer '2.2.0' }
                    it.because 'Gradle depends on ivy implementation details which changed with newer versions'
                }
            }
        }
    }
}

def downgradeTestNG(ComponentMetadataHandler handler, String module) {
    handler.withModule(module) {
        allVariants {
            withDependencyConstraints {
                it.findAll { it.group == 'org.testng' }.each {
                    it.version { prefer '6.3.1' }
                    it.because '6.3.1 is required by Gradle and part of the distribution'
                }
            }
        }
    }
}

def downgradeXmlApis(ComponentMetadataHandler handler, String module) {
    handler.withModule(module) {
        allVariants {
            withDependencies {
                it.findAll { it.group == 'xml-apis' }.each {
                    it.version { prefer '1.4.01' }
                    it.because 'Gradle has trouble with the versioning scheme and pom redirects in higher versions'
                }
            }
        }
    }
}
