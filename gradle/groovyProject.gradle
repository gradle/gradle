import java.util.jar.Attributes

apply plugin: 'groovy'

archivesBaseName = "gradle-${name.replaceAll("\\p{Upper}") { "-${it.toLowerCase()}" }}"

sourceCompatibility = 1.5
targetCompatibility = 1.5

ext {
    compileTasks = tasks.matching { it instanceof JavaCompile || it instanceof GroovyCompile }
    testTasks = tasks.withType(Test)
    generatedResourcesDir = file("$buildDir/generated-resources/main")
    generatedTestResourcesDir = file("$buildDir/generated-resources/test")
    jarTasks = tasks.withType(Jar)
}

dependencies {
    testCompile libraries.junit, libraries.jmock, libraries.spock
}

// Extracted as it's also used by buildSrc
apply from: "$rootDir/gradle/compile.gradle"

task classpathManifest(type: ClasspathManifest)

sourceSets {
    main.output.dir generatedResourcesDir, builtBy: classpathManifest
}

if (!javaVersion.java6Compatible) {
    sourceSets.all {
        java.exclude '**/jdk6/**'
        groovy.exclude '**/jdk6/**'
    }
}
if (!javaVersion.java7Compatible) {
    sourceSets.all {
        java.exclude '**/jdk7/**'
        groovy.exclude '**/jdk7/**'
    }
}

testTasks.all { task ->
    if (isCiServer) {
        maxParallelForks = rootProject.maxParallelForks
        systemProperties['org.gradle.test.maxParallelForks'] = maxParallelForks
        systemProperties['org.gradle.ci.agentCount'] = 2
        systemProperties['org.gradle.ci.agentNum'] = rootProject.agentNum
        doFirst {
            println "maxParallelForks for '$task.path' is $task.maxParallelForks"
        }
    }
}

jarTasks.all { jar ->
    jar.manifest.mainAttributes(
        (Attributes.Name.IMPLEMENTATION_TITLE.toString()): 'Gradle',
        (Attributes.Name.IMPLEMENTATION_VERSION.toString()): version,
    )
}

// Configures the project to use the test fixtures from another project, which by default is core.
// Note this is not used to provide test fixtures, see gradle/testFixtures.gradle for that
ext.useTestFixtures = { params = [:] ->
    def projectPath = params.project ?: ":core"
    def sourceSet = params.sourceSet ?: "test"
    def compileConfiguration = sourceSet == "main" ? "compile" : "${sourceSet}Compile"
    def runtimeConfiguration = sourceSet == "main" ? "runtime" : "${sourceSet}Runtime"

    dependencies {
        add(compileConfiguration, project(path: projectPath, configuration: "testFixturesUsageCompile"))
        add(compileConfiguration, project(':internalTesting'))
        add(runtimeConfiguration, project(path: projectPath, configuration: "testFixturesUsageRuntime"))
    }
}

if (file("src/testFixtures").exists()) {
    apply from: "$rootDir/gradle/testFixtures.gradle"
}

apply from: "$rootDir/gradle/integTest.gradle"

class ClasspathManifest extends DefaultTask {

    FileCollection input = project.configurations.runtime

    @OutputFile
    File getManifestFile() {
        return new File(project.generatedResourcesDir, "${project.archivesBaseName}-classpath.properties")
    }

    @Input
    Properties getProperties() {
        def properties = new Properties()
        properties.runtime = input.fileCollection {
            (it instanceof ExternalDependency) || (it instanceof FileCollectionDependency)
        }.collect { it.name }.join(',')
        properties.projects = input.allDependencies.withType(ProjectDependency).collect { it.dependencyProject.archivesBaseName }.join(',')
        return properties
    }

    @TaskAction
    def generate() {
        manifestFile.withOutputStream { properties.save(it, 'module definition') }
    }
}

ext.ClasspathManifest = ClasspathManifest

// This is here mostly for the TravisCI build.
// It advertises as an ANSI capable terminal but isn't, so we run with -q there to make
// the output legible, but we still want test result info
if (buildTypes.pullRequestValidation.active) {
    tasks.withType(Test) {
        testLogging {
            quiet {
                events "failed"
                exceptionFormat "full"
            }
        }
    }
}

task compileAll {
    dependsOn tasks.matching { it instanceof JavaCompile || it instanceof GroovyCompile }
}

test {
    jvmArgs "-XX:MaxPermSize=512m"
}
