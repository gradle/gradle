/*
 * Copyright 2012 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

evaluationDependsOn ":distributions"
evaluationDependsOn ":docs"

task intTestImage(type: Sync) {
    into file("$buildDir/integ test")
}

// For intTestImage we use the distribution we want to test and replace `build-version.jar` with the one with a test version inside.
// This enables us to cache the integration tests.
// We do this no matter if we have incoming distribution or not.

if (useIncomingDistributions) {
    task unpackIncomingAllDistribution(type: Sync) {
        from zipTree(incomingAllDistribution)
        into "$buildDir/tmp/unpacked-incoming-all-distribution"
    }

    // Compensate for the top level dir in the zip
    def unpackedPath = "$unpackIncomingAllDistribution.destinationDir/gradle-${incomingDistributionsBuildReceipt.versionNumber}"

    intTestImage {
        dependsOn unpackIncomingAllDistribution
        from(unpackedPath)
    }
} else {
    intTestImage {
        with project(":distributions").binDistImage
        into "samples", {
            from { project(":docs").outputs.samples }
        }
        doLast { task ->
            ant.chmod(dir: "$destinationDir/bin", perm: "ugo+rx", includes: "**/*")
        }
    }
}
