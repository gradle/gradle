/*
 * Copyright 2016 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import gradlebuild.performance.generator.tasks.BuildBuilderGenerator
import gradlebuild.performance.generator.tasks.CppMultiProjectGeneratorTask
import gradlebuild.performance.generator.tasks.JavaExecProjectGeneratorTask
import gradlebuild.performance.generator.tasks.JvmProjectGeneratorTask
import gradlebuild.performance.generator.tasks.KtsProjectGeneratorTask
import gradlebuild.performance.generator.tasks.MonolithicNativeProjectGeneratorTask
import gradlebuild.performance.generator.tasks.NativeProjectGeneratorTask
import gradlebuild.performance.generator.tasks.NativeProjectWithDepsGeneratorTask
import gradlebuild.performance.generator.tasks.RemoteProject

import static gradlebuild.basics.BuildEnvironmentKt.repoRoot
import static gradlebuild.basics.BuildParamsKt.getBuildCommitId

// === Gradle Build ===
performanceTest.registerTestProject("gradleBuildCurrent", RemoteProject) {
    remoteUri = repoRoot(project).asFile.absolutePath
    ref = getBuildCommitId(project)
}

performanceTest.registerTestProject("gradleBuildBaseline", RemoteProject) {
    remoteUri = repoRoot(project).asFile.absolutePath
    // Remember to update accordingly when rebasing/squashing
    // Do not use the "Rebase and merge" nor "Squash and merge" Github buttons when merging a PR that change the baseline
    ref = '264bd7744d2e89b2708303cd56bb11bc5003fa7e'
}

// === Declarative DSL ===
performanceTest.registerTestProject("largeEmptyMultiProjectDeclarativeDsl", JavaExecProjectGeneratorTask) {
    outputs.dir new File(buildDir, "largeEmptyMultiProjectDeclarativeDsl")
    outputs.cacheIf { true }
    inputs.property('template', "largeEmptyMultiProjectDeclarativeDsl")
    inputs.property('args') { // Arguments are covered by the testProjectName and the outputs. We don't want them to contain the absolute path
        return []
    }
    classpath = sourceSets.performanceTest.runtimeClasspath.filter { it.name.contains('groovy') || it.name.contains('guava')  || it.name.contains('gradle-') }
    mainClass = 'org.gradle.performance.generator.DeclarativeDslTestProjectGenerator'
    args name, buildDir.absolutePath
}



// === Java ===
['largeMonolithicJavaProject', 'largeJavaMultiProject', 'smallJavaMultiProjectManyExternalDependencies', 'largeJavaMultiProjectHierarchy', 'largeMonolithicGroovyProject', 'largeGroovyMultiProject', 'largeJavaMultiProjectNoBuildSrc', 'largeJavaMultiProjectKotlinDsl',
 'mediumMonolithicJavaProject', 'mediumJavaMultiProject', 'mediumJavaMultiProjectWithTestNG', 'mediumJavaCompositeBuild', 'mediumJavaPredefinedCompositeBuild',
 'smallJavaMultiProject', 'smallJavaMultiProjectNoBuildSrc'].each { template ->
    performanceTest.registerTestProject(template, JavaExecProjectGeneratorTask) {
        outputs.dir new File(buildDir, template)
        outputs.cacheIf { true }
        inputs.property('template', template)
        inputs.property('args') { // Arguments are covered by the testProjectName and the outputs. We don't want them to contain the absolute path
            return []
        }
        classpath = sourceSets.performanceTest.runtimeClasspath.filter { it.name.contains('groovy') || it.name.contains('guava')  || it.name.contains('gradle-') }
        mainClass = 'org.gradle.performance.generator.TestProjectGenerator'
        args name, buildDir.absolutePath
    }
}

// TODO implement VerboseTestOutputPerformanceTest with static project
performanceTest.registerTestProject("withVerboseTestNG", JvmProjectGeneratorTask) {
    projects = 1
    sourceFiles = 10
    subProjectTemplates << 'with-verbose-testng'
    daemonMemory = '256m'
}
performanceTest.registerTestProject("withVerboseJUnit", JvmProjectGeneratorTask) {
    projects = 1
    sourceFiles = 10
    subProjectTemplates << 'with-verbose-junit'
    daemonMemory = '256m'
}

//TODO replace with direct task objects creation in test
performanceTest.registerTestProject("createLotsOfTasks", JvmProjectGeneratorTask) {
    projects = 1
    sourceFiles = 0
    subProjectTemplates = ['task-creation', 'gradle-properties']
    templateArgs = [
        taskCount: 15000
    ]
    dependencyGraph {
        size = 0
    }

    numberOfExternalDependencies = 0
    daemonMemory = '1G'
}

performanceTest.registerTestProject("generateLotsOfDeprecationWarnings", Copy) {
    into "build/$name"
    from "src/templates/$name"
}


// === Gradle Kotlin DSL ===
performanceTest.registerTestProject("ktsManyProjects", KtsProjectGeneratorTask) {
    projects = 100
    sourceFiles = 0
    daemonMemory = '512m'
}

performanceTest.registerTestProject("ktsSmall", KtsProjectGeneratorTask) {
    daemonMemory = '512m'
}

// === Native Software Model ===
performanceTest.registerTestProject("smallNative", NativeProjectGeneratorTask) {
    projects = 1
    sourceFiles = 20
    templateArgs = [
        moduleCount: 1,
        functionCount: 1
    ]
    subProjectTemplates = ['native-source', 'native-component', 'gradle-properties']
    daemonMemory = '256m'
}

performanceTest.registerTestProject("mediumNative", NativeProjectGeneratorTask) {
    projects = 1
    sourceFiles = 100
    templateArgs = [
        moduleCount: 1,
        functionCount: 20
    ]
    subProjectTemplates = ['native-source', 'native-component', 'gradle-properties']
    daemonMemory = '256m'
}

performanceTest.registerTestProject("bigNative", NativeProjectGeneratorTask) {
    projects = 1
    sourceFiles = 500
    templateArgs = [
        moduleCount: 1,
        functionCount: 50
    ]
    subProjectTemplates = ['native-source', 'native-component', 'gradle-properties']
    daemonMemory = '1G'
}

performanceTest.registerTestProject("multiNative", NativeProjectGeneratorTask) {
    projects = 10
    sourceFiles = 20
    templateArgs = [
        moduleCount: 10,
        functionCount: 20
    ]
    subProjectTemplates = ['native-source', 'native-component']
    daemonMemory = '256m'
}

performanceTest.registerTestProject("manyProjectsNative", NativeProjectGeneratorTask) {
    projects = 100
    sourceFiles = 1
    templateArgs = [
        moduleCount: 3,
        functionCount: 1
    ]
    subProjectTemplates = ['native-source', 'native-component']
    daemonMemory = '1G'
    maxWorkers = 12
}

performanceTest.registerTestProject("smallPCHNative", NativeProjectGeneratorTask) {
    projects = 1
    sourceFiles = 20
    templateArgs = [
        moduleCount: 1,
        functionCount: 1
    ]
    subProjectTemplates = ['native-pch-source', 'native-pch-component', 'gradle-properties']
    daemonMemory = '256m'
}

performanceTest.registerTestProject("mediumPCHNative", NativeProjectGeneratorTask) {
    projects = 1
    sourceFiles = 200
    templateArgs = [
        moduleCount: 1,
        functionCount: 1
    ]
    subProjectTemplates = ['native-pch-source', 'native-pch-component', 'gradle-properties']
    daemonMemory = '256m'
}

performanceTest.registerTestProject("bigPCHNative", NativeProjectGeneratorTask) {
    projects = 1
    sourceFiles = 1000
    templateArgs = [
        moduleCount: 1,
        functionCount: 1
    ]
    subProjectTemplates = ['native-pch-source', 'native-pch-component', 'gradle-properties']
    daemonMemory = '1G'
}

performanceTest.registerTestProject("nativeMonolithicOverlapping", MonolithicNativeProjectGeneratorTask) {
    templateArgs = [overlapWithOutput: true]
    projects = 100
    // number of source files to generate in each project of each type
    // this generates 200 C, 200 C++, 200 headers and 400 unused C/C++ files
    sourceFiles = 200
    daemonMemory = '2560m'
}

performanceTest.registerTestProject("nativeMonolithic", MonolithicNativeProjectGeneratorTask) {
    templateArgs = [overlapWithOutput: false]
    projects = 100
    sourceFiles = 200
    daemonMemory = '2560m'
}

performanceTest.registerTestProject("smallNativeMonolithic", MonolithicNativeProjectGeneratorTask) {
    templateArgs = [overlapWithOutput: false]
    projects = 2
    sourceFiles = 100
    daemonMemory = '512m'
}

performanceTest.registerTestProject("mediumNativeMonolithic", MonolithicNativeProjectGeneratorTask) {
    templateArgs = [overlapWithOutput: false]
    projects = 10
    sourceFiles = 200
    daemonMemory = '512m'
}

["nativeMonolithicOverlapping", "nativeMonolithic", "smallNativeMonolithic", "mediumNativeMonolithic"].collect { tasks.withType(MonolithicNativeProjectGeneratorTask).named(it) }*.configure {
    templateArgs += [
        // number of functions created in each source file
        functionCount: 50,
        // number of prebuilt libraries to generate
        prebuiltLibraries: 30,
        // number of files included in each source set
        includedSourceCount: 50,
        // number of headers each source file includes
        includedHeaderCount: 10,
        // number of 'common' headers each source file includes
        includedCommonHeaderCount: 10,
    ]
    rootProjectTemplates = ['native-monolithic', 'gradle-properties']
    subProjectTemplates = []
    additionalProjectFiles = ['common.gradle', 'prebuilt.gradle', 'components.gradle']
    maxWorkers = 12
}

performanceTest.registerTestProject("nativeDependents", NativeProjectWithDepsGeneratorTask) {
    resources.from(fileTree(resolveTemplate("native-dependents-resources")))
    resources.from(resolveTemplate("init.gradle"))
    daemonMemory = '3G'
}

performanceTest.registerTestProject("nativeDependentsDeep", NativeProjectWithDepsGeneratorTask) {
    resources.from(fileTree(resolveTemplate("native-dependents-resources")))
    resources.from(resolveTemplate("init.gradle"))
    generateDeepHierarchy = true
    daemonMemory = '3G'
}

// === Native Current Model Plugins ===
performanceTest.registerTestProject("smallCppApp", CppMultiProjectGeneratorTask) {
    projects = 1
    sourceFiles = 20
    templateArgs = [
        functionCount: 2
    ]
    subProjectTemplates = ['cpp-source', 'cpp-project', 'gradle-properties']
    daemonMemory = '256m'
}

performanceTest.registerTestProject("mediumCppApp", CppMultiProjectGeneratorTask) {
    projects = 1
    sourceFiles = 100
    templateArgs = [
        functionCount: 20
    ]
    subProjectTemplates = ['cpp-source', 'cpp-project', 'gradle-properties']
    daemonMemory = '256m'
}

performanceTest.registerTestProject("mediumCppAppWithMacroIncludes", CppMultiProjectGeneratorTask) {
    projects = 1
    sourceFiles = 100
    templateArgs = [
        functionCount: 20,
        useMacroIncludes: true

    ]
    subProjectTemplates = ['cpp-source', 'cpp-project', 'gradle-properties']
    daemonMemory = '256m'
}

performanceTest.registerTestProject("bigCppApp", CppMultiProjectGeneratorTask) {
    projects = 1
    sourceFiles = 500
    templateArgs = [
        functionCount: 50
    ]
    subProjectTemplates = ['cpp-source', 'cpp-project', 'gradle-properties']
    daemonMemory = '256m'
}

performanceTest.registerTestProject("smallCppMulti", CppMultiProjectGeneratorTask) {
    projects = 11
    sourceFiles = 2
    templateArgs = [
        functionCount: 1
    ]
    subProjectTemplates = ['cpp-source', 'cpp-project']
    daemonMemory = '256m'
}

performanceTest.registerTestProject("mediumCppMulti", CppMultiProjectGeneratorTask) {
    projects = 101
    sourceFiles = 4
    templateArgs = [
        functionCount: 2
    ]
    subProjectTemplates = ['cpp-source', 'cpp-project']
    daemonMemory = '256m'
}

performanceTest.registerTestProject("mediumCppMultiWithMacroIncludes", CppMultiProjectGeneratorTask) {
    projects = 101
    sourceFiles = 4
    templateArgs = [
        functionCount: 2,
        useMacroIncludes: true
    ]
    subProjectTemplates = ['cpp-source', 'cpp-project']
    daemonMemory = '256m'
}

performanceTest.registerTestProject("bigCppMulti", CppMultiProjectGeneratorTask) {
    projects = 201
    sourceFiles = 8
    templateArgs = [
        functionCount: 3
    ]
    subProjectTemplates = ['cpp-source', 'cpp-project']
    daemonMemory = '1G'
}

// Build-builder projects
tasks.register("buildBuilder", RemoteProject) {
    remoteUri = 'https://github.com/gradle/build-builder.git'
    // From master branch
    ref = '39037ebf30b9eab5bdceff259ce50126f94a5561'
}

tasks.register("installBuildBuilder", GradleBuild) {
    dir = buildBuilder.outputDirectory
    tasks = ['install']
    dependsOn buildBuilder
}

tasks.withType(BuildBuilderGenerator).configureEach {
    buildBuilderInstall = new File(tasks.buildBuilder.outputDirectory, "build/install/build-builder")
    dependsOn installBuildBuilder
}

performanceTest.registerTestProject("mediumSwiftMulti", BuildBuilderGenerator) {
    projectType = "swift"
    projects = 100
    sourceFiles = 4
    description = "Generates a ${projectType} build using build-builder (${projects} projects, ${sourceFiles} files)"
    doLast {
        def projectDirectory = generatedDir.get().asFile
        setMaxWorkers(projectDirectory, 6)
        setMaxHeap(projectDirectory, "1G")
        setParallel(projectDirectory, true)
    }
}

performanceTest.registerTestProject("bigSwiftApp", BuildBuilderGenerator) {
    projectType = "swift"
    projects = 1
    sourceFiles = 500
    description = "Generates a ${projectType} build using build-builder (${projects} projects, ${sourceFiles} files)"

    doLast {
        def projectDirectory = generatedDir.get().asFile
        setMaxWorkers(projectDirectory, 6)
        setMaxHeap(projectDirectory, "1G")
        setParallel(projectDirectory, true)
        def buildFile = new File(projectDirectory, "build.gradle")
        buildFile << """
            tasks.withType(SwiftCompile) {
                compilerArgs.addAll provider {
                    [ "-num-threads", "6", "-j", "6" ]
                }
            }
        """
    }
}

// === Android ===
performanceTest.registerAndroidTestProject("largeAndroidBuild", RemoteProject) {
    remoteUri = 'https://github.com/gradle/perf-android-large.git'
    // From android-73 branch
    ref = "6f56fcdbaade0a9ce43777cf91ab8bf79cd14dd0"
}

performanceTest.registerAndroidTestProject("largeAndroidBuild2", RemoteProject) {
    remoteUri = 'https://github.com/gradle/perf-android-large-2.git'
    // Pinned from android-73-kotlin-16 branch
    ref = "16d64faa6055a281874329ff95fbcdd6861b7248"
}

performanceTest.registerAndroidTestProject("nowInAndroidBuild", RemoteProject) {
    remoteUri = 'https://github.com/gradle/nowinandroid.git'
    // Pinned from main branch
    ref = "5bd2b5e7f5c24610b896618496f7bbba393d4c96" // based on b85cf7d4766ca84a424700f86a96f5eab0a3cfbd latest of May 14th, 2024
    doLast {
        setMaxWorkers(outputDirectory, 8)
    }
}

performanceTest.registerAndroidTestProject("santaTrackerAndroidBuild", RemoteProject) {
    remoteUri = 'https://github.com/gradle/santa-tracker-android.git'
    // Pinned from main branch
    ref = 'e9419cad3583427caca97958301ff98fc8e9a1c3'
    doLast {
        addGoogleServicesJson(outputDirectory)
    }
}

performanceTest.registerAndroidTestProject("uberMobileApp", RemoteProject) {
    remoteUri = 'https://github.com/gradle/android-build-eval.git'
    subdirectory = 'mobile_app1'
    // Pinned from PR https://github.com/uber-common/android-build-eval/pull/17
    ref = '18ff193198010064e43e9244edde91ab54c2da70'
    doLast(objects.newInstance(DeleteOkBuckDirectory))
}

abstract class DeleteOkBuckDirectory implements Action<RemoteProject> {
    @Inject
    abstract FileSystemOperations getFileSystemOperations()

    void execute(RemoteProject remoteProject) {
        fileSystemOperations.delete {
            delete(new File(remoteProject.outputDirectory, ".okbuck"))
        }
    }
}

private void setMaxWorkers(File projectDirectory, int maxWorkers) {
    new File(projectDirectory, "gradle.properties") << "\norg.gradle.workers.max=${maxWorkers}\n"

}

private void setMaxHeap(File projectDirectory, String maxHeap) {
    new File(projectDirectory, "gradle.properties") << "\norg.gradle.jvmargs=-Xmx${maxHeap}\n"

}

private void setParallel(File projectDirectory, boolean parallel) {
    new File(projectDirectory, "gradle.properties") << "\norg.gradle.parallel=${parallel}\n"

}

private void addGoogleServicesJson(File outputDirectory) {
    new File(outputDirectory, 'santa-tracker/google-services.json').text = """
{
  "project_info": {
    "project_number": "012345678912",
    "firebase_url": "https://example.com",
    "project_id": "example",
    "storage_bucket": "example.example.com"
  },
  "client": [
    {
      "client_info": {
        "mobilesdk_app_id": "1:012345678912:android:0123456789abcdef",
        "android_client_info": {
          "package_name": "com.google.android.apps.santatracker.debug"
        }
      },
      "oauth_client": [
        {
          "client_id": "foo.example.com",
          "client_type": 3
        }
      ],
      "api_key": [
        {
          "current_key": "012345678901234567890123456789012345678"
        }
      ],
      "services": {
        "analytics_service": {
          "status": 1
        },
        "appinvite_service": {
          "status": 1,
          "other_platform_oauth_client": []
        },
        "ads_service": {
          "status": 2
        }
      }
    },
    {
      "client_info": {
        "mobilesdk_app_id": "1:012345678912:android:0123456789abcdef",
        "android_client_info": {
          "package_name": "com.google.android.apps.santatracker.debug"
        }
      },
      "oauth_client": [
        {
          "client_id": "foo.example.com",
          "client_type": 3
        }
      ],
      "api_key": [
        {
          "current_key": "012345678901234567890123456789012345678"
        }
      ],
      "services": {
        "analytics_service": {
          "status": 1
        },
        "appinvite_service": {
          "status": 1,
          "other_platform_oauth_client": []
        },
        "ads_service": {
          "status": 2
        }
      }
    }
  ],
  "configuration_version": "1"
}
"""
}

performanceTest.registerTestProject("excludeRuleMergingBuild", RemoteProject) {
    remoteUri = 'https://github.com/gradle/performance-comparisons.git'
    // From master branch
    ref = "b0eb05ab058e55e3aad5935d499c9079284c2592"
    subdirectory = 'exclude-merging'
}

performanceTest.registerTestProject("springBootApp", RemoteProject) {
    remoteUri = 'https://github.com/gradle/performance-comparisons.git'
    // From master branch
    ref = "e53dc7182fafcf9fedf07920cbbea8b40ee4eef4"
    subdirectory = 'parallel-downloads'
}

performanceTest.registerTestProject("largeNativeBuild", RemoteProject) {
    remoteUri = 'https://github.com/gradle/perf-native-large'
    // From master branch
    ref = "08a06b5f4943a5a892a178baddc27e1884e7c03c"
    finalizedBy 'largeNativeBuildPrebuilt'
}

performanceTest.registerTestProject("largeNativeBuildPrebuilt", GradleBuild) {
    buildFile = file("$largeNativeBuild.outputDirectory/prebuilt/util/build.gradle")
    dir = file("$largeNativeBuild.outputDirectory/prebuilt/util")
    tasks = ['build']
}

performanceTest.registerTestProject("archivePerformanceProject", Copy) {
    into "build/$name"
    from "src/templates/$name"
}

performanceTest.registerTestProject("workerApiProject", Copy) {
    into "build/$name"
    from "src/templates/$name"
}

tasks.withType(JvmProjectGeneratorTask).configureEach {
    if (project.hasProperty("springDmPluginVersion")) {
        templateArgs['springDmPluginVersion'] = springDmPluginVersion
    }
    templateArgs['projectDependencies'] = true
}
